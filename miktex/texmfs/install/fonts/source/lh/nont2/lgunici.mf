%
% lgunici.mf
%
%% Cyrillic font container with T2 encoding beta-support
%
% This file is future part of lxfonts package
% Version 3.5 // Patchlevel=1
% (c) O.Lapko
%
% This package is freeware product under conditions similar to
% those of D. E. Knuth specified for the Computer Modern family of fonts.
% In particular, only the authors are entitled to modify this file
% (and all this package as well) and to save it under the same name.
%
% Content:
%
% Italic non-T2 Cyrillic letters included in Unicode
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% List of letternames and their codes in Unicode
%
% e_grv         "50     E with grave
% i_grv         "5d     I with grave
% gje           "53     Ghe with acute
% kje           "5c     Ka with acute
% zh_brv        "c2     Zhe with breve
% a_brv         "d1     A with breve
% a_uml         "d3     A with umlaut
% e_brv         "d7     E with breve
% schwa_uml     "db     Schwa with umlaut
% zh_uml        "dd     Zhe with umlaut
% z_uml         "df     Ze with umlaut
% i_bar         "e3     I with macrn (bar)
% i_uml         "e5     I with umlaut
% o_uml         "e7     O with umlaut
% otld_uml      "e9     O tilded with umlaut
% ukre_uml      --      Ukrainian E with umlaut
% erev_uml      "ed     Erev with umlaut
% u_bar         "ef     U with macron (bar)
% u_uml         "f1     U with umlaut
% u_dac         "f3     U with double acute
% ch_uml        "f5     Che with umlaut
% ery_uml       "f9     Ery with umlaut
%

LHver_check(3,5); % like |version_check| in ec

lhchar "Italic Cyrillic letter e_grv - e grave";
cyrchar_twice(e_grv,8u#,x_height#,0);
italcorr max(1/3x_height#*slant,x_height#*slant+.5(.2[hair#,stem#])-u#); %e
adjust_fit(if monospace:0 else:-.35u#fi,0);
% ital_e;
pickup fine.nib; %numeric heavy_hair; heavy_hair=hround .2[hair,stem];%defined in lxpseudo
pos0(vair,-90); pos1(heavy_hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x0=rt x3l; rt x1r=hround(w-1.5u+.5heavy_hair); x2=x4=.5(w+u);
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y0=y3=y6=bar_height; y1=.5[y0,y2]; top y2r=h+oo; bot y4r=-oo;
top y5l=vround .5bar_height; path p; p=z4{right}..z5..z6;
filldraw stroke z0e{right}...z1e{up}...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
% the accent
put_gravis; repeat_once;
penlabels(0,1,2,3,4,5,6); endchar;

lhchar "Italic Cyrillic letter i_grv - i grave";
cyrchar_twice(i_grv,10u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0);
% ital_i;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left skewstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
% the accent
put_gravis; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter kje (k acute)";
cyrchar_twice(kje,9u#,x_height#,0); %+acc_height#
italcorr x_height#*slant; %k
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_k;
pickup fine.nib; x2-.5stem=hround(2.5u-.5stem);
%ihalfstem;
if monospace: pos2(stem,0); top y2=h;
else: x0=0; hook_in(0,1,2); fi % opening hook
pos2'(stem,0); pos11(stem,0); z2'=z2; x11=x2'; y11-.5stem=-oo;
filldraw circ_stroke z11e--z2'e;  % left stem
pos3(vair,90); x3=x11; y3=bar_height;
pos4(vair,90); x4=w-2u; top y4r=x_height+oo;
filldraw stroke z3e{right}..{right}z4e;  % upper diagonal
pos5(hair,0); pos6(flare,0);
rt x5r=hround(w-.5u); y5+.5flare=vround(bot y4l-.03x_height);
bulb(4,5,6);  % bulb
pos7(stem,0); pos8(vair,90); pos9(hair,180);
x9+.5hair=hround(w+.5hair-eps); y9=1/3x_height;
lft x7l=hround(w-2.75u-.5stem); y7=1/2y3; x8=w-1.2u; bot y8l=-oo;
filldraw stroke z3e{right}...z7e{down}
 ...z8e{right}...{up}z9e;  % lower diagonal
%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter gje (g acute)";
cyrchar_twice(gje,6.25u#,x_height#,0); %+acc_height#
italcorr max(1/3x_height#*slant+.5hair#+.5u#,h#*slant+.5flare#-2u#); %e
adjust_fit(if monospace:u#,u# else: 0.5u#,0.5u# fi);
full_h:=h; h:=x_height;
% ital_g;
pickup fine.nib; x0l=hround eps; x0'=x0; x5r=good.x(w-eps); x6=x5;
x2=.5w+2u; x3=.5w-2u; x1=x4=x7=.5[x3l,x2r]; y7=.5[y2,y3];
h-y0=y5; top y5l=vround .5bar_height; y6=bar_height; y0'=h-bar_height;
h-y2=y3=1/4x_height; h-y1=y4; bot y4r=-oo;
pos0(hair,320); pos1(vair,270); pos2(stem,180);
pos3(stem,180); pos4(vair,270); pos5(hair,320); pos7(stem,135);
path p.l,p.r; p.l=z0'..z0..z1{right}; p.r=z4{right}..z5..z6;
filldraw stroke z0e{direction 1 of p.l}
 ...z1e{right}...{down}z2e...z7e...z3e{down}...z4e{right}
 ...{direction 1 of p.r}z5e; % main stroke
%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter zh_brv - zhe breve";
cyrchar(zh_brv,20u#,x_height#+acc_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi; %c
adjust_fit(-.5u#,0 if monospace:-.5u# fi); %zh_adj
full_h:=h; h:=x_height;
% ital_zh;
if odd(fudged.stem-w): change_width; fi % symmetric & all three equal - AS
pickup fine.nib; pos1(flare,0); pos2(hair,0); pos3(vair,90);
pos4(curve,180); pos5(vair,270); pos6(hair,320);
if monospace: x3=x5=.5[lft x4r,rt x2r]-eps; rt x2r=good.x (w+eps);
else: x3=x5=good.x(w-3.5u); rt x2r=hround max(rt x3,w-u)+eps; fi
lft x4r=hround max(.5w+.5stem+.5,w-6.5u-.5curve+eps); x6r=good.x(w+eps); x0=x6;
y2=.5[bar_height,h]; top y3r=h+oo; bulb(3,2,1);  % bulb
bot y5r=-oo; y4=.5[y3,y5]; top y6l=vround .5bar_height; y0=bar_height;
path p; p=z5{right}..z6..z0;
filldraw stroke pulled_arc.e(3,4)
 & pulled_arc.e(4,5)...{direction 1 of p}z6e;  % right arc
pos12(flare,180); pos11(hair,180); pos10(vair,270);
pos9(curve,360); pos8(vair,450); pos7(hair,500);
if monospace: lft x11r=good.x (-eps); x10=x8=.5[lft x11r,rt x9r]+eps;
else: x10=x8=3.5u; lft x11r=hround min(lft x10,u)-eps; fi
rt x9r=hround min(.5w-.5stem-.5,6.5u+.5curve-eps);
x7r=good.x (if not monospace:rt x11r-.67u fi-eps); x0'=x7;
y11=h-.5[bar_height,h]; bot y10r=-oo; bulb(10,11,12);  % bulb
top y8r=h+oo; y9=.5[y10,y8]; bot y7l=vround (h-.65bar_height); y0'=bar_height;
path p_; p_=z8{left}..z7..z0;
filldraw stroke pulled_arc.e(10,9)
 & pulled_arc.e(9,8)...{direction 1 of p_}z7e;  % left arc
y13=bar_height; x13=x4; y14=y13; x14=w-x4;  x15=x16=.5w;
y15+.5stem=h+oo; bot y16=0;
pos13(vair,90); pos14(vair,90);  pos15(fudged.stem,0); pos16(fudged.stem,0);
filldraw stroke z13e--z14e; filldraw circ_stroke z15e--z16e; % stem & cross
%%%
% the accent
h:=full_h; lowercase_breve(.5w,0, 17,18,19);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16, 17,18,19); endchar;

lhchar "Italic Cyrillic letter a_brv - a breve";
cyrchar(a_brv,9u#,x_height#+acc_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:0 else:-.35u#fi,0); %Cyrillic adj
full_h:=h; h:=x_height;
% ital_a;
pickup fine.nib;
pos0(hair,0); pos1(vair,90); pos2(curve,180); pos3(vair,270);
pos4(stem,0); x4=x0; y4+.5stem=vround .98h;
x1=x3=.5[x0,x2]; lft x2r=hround(1.5u-.5curve);
rt x4r=hround(w-2.5u+.5stem);
top y1r=h+oo; bot y3r=-oo; y0=y2=.5[y1,y3];
filldraw stroke super_arc.e(0,1) & pulled_arc.e(1,2)
 & pulled_arc.e(2,3) & super_arc.e(3,0);  % bowl
x5=x4; x7=w; hook_out(5,6,7);  % closing hook
filldraw circ_stroke z4e--z5e;  % stem
% the accent
h:=full_h; lowercase_breve(.5w,0, 8,9,10); %x1r y1r-h
penlabels(0,1,2,3,4,5,6,7, 8,9,10); endchar;

lhchar "Italic Cyrillic letter a_uml - a umlaut";
cyrchar(a_uml,9u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:0 else:-.35u#fi,0); %Cyrillic adj
full_h:=h; h:=x_height;
% ital_a;
pickup fine.nib;
pos0(hair,0); pos1(vair,90); pos2(curve,180); pos3(vair,270);
pos4(stem,0); x4=x0; y4+.5stem=vround .98h;
x1=x3=.5[x0,x2]; lft x2r=hround(1.5u-.5curve);
rt x4r=hround(w-2.5u+.5stem);
top y1r=h+oo; bot y3r=-oo; y0=y2=.5[y1,y3];
filldraw stroke super_arc.e(0,1) & pulled_arc.e(1,2)
 & pulled_arc.e(2,3) & super_arc.e(3,0);  % bowl
x5=x4; x7=w; hook_out(5,6,7);  % closing hook
filldraw circ_stroke z4e--z5e;  % stem
% the accent
h:=full_h; lowercase_umlaut(0,0, 8,9,10,11);
penlabels(0,1,2,3,4,5,6,7, 8,9,10,11); endchar;

lhchar "Italic Cyrillic letter e_brv - e breve";
cyrchar(e_brv,8u#,x_height#+acc_height#,0);
italcorr max(1/3x_height#*slant,x_height#*slant+.5(.2[hair#,stem#])-u#); %e
adjust_fit(if monospace:0 else:-.35u#fi,0); full_h:=h; h:=x_height;
% ital_e;
pickup fine.nib; %numeric heavy_hair; heavy_hair=hround .2[hair,stem];%defined in lxpseudo
pos0(vair,-90); pos1(heavy_hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x0=rt x3l; rt x1r=hround(w-1.5u+.5heavy_hair); x2=x4=.5(w+u);
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y0=y3=y6=bar_height; y1=.5[y0,y2]; top y2r=h+oo; bot y4r=-oo;
top y5l=vround .5bar_height; path p; p=z4{right}..z5..z6;
filldraw stroke z0e{right}...z1e{up}...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
% the accent
h:=full_h; lowercase_breve(.5w,0, 7,8,9);
penlabels(1,2,3,4,5,6, 7,8,9); endchar;

lhchar "Italic Cyrillic letter schwa_uml - schwa umlaut";
cyrchar(schwa_uml,8u#,lc_trema_height#,0);
italcorr max(1/3x_height#*slant,x_height#*slant+.5(.2[hair#,stem#])-u#);
adjust_fit(0,if monospace:0 else:-.35u#fi); full_h:=h; h:=x_height;
% ital_schwa;
pickup fine.nib; %numeric heavy_hair; heavy_hair=hround .2[hair,stem];%defined in lxpseudo
pos0(vair,90); pos1(heavy_hair,180); pos2(vair,-90);
pos3(curve,0); pos4(vair,90); pos5(hair,140);
x0=lft x3l; lft x1r=hround(1u-.5heavy_hair); x2+.5u=x4=.5(w-u);
rt x3r=hround(w-1.5u+.5curve);
lft x5r=good.x (lft x1r+eps); x6=x5;
y0=y3=y6=bar_height; y1=.5[y0,y2]; bot y2r=-oo; top y4r=h+oo;
top y5r=vround .5[bar_height,x_height]; path p; p=z4{left}..z5..z6;
filldraw stroke z0e{left}...z1e{down}...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
% the accent
h:=full_h; lowercase_umlaut(0,0, 7,8,9,10);
penlabels(1,2,3,4,5,6, 7,8,9,10); endchar;

lhchar "Italic Cyrillic letter zh_uml - zhe umlaut";
cyrchar(zh_uml,20u#,lc_trema_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi; %c
adjust_fit(-.5u#,0 if monospace:-.5u# fi); %zh_adj
h:=x_height;
% ital_zh;
if odd(fudged.stem-w): change_width; fi % symmetric & all three equal - AS
pickup fine.nib; pos1(flare,0); pos2(hair,0); pos3(vair,90);
pos4(curve,180); pos5(vair,270); pos6(hair,320);
if monospace: x3=x5=.5[lft x4r,rt x2r]-eps; rt x2r=good.x (w+eps);
else: x3=x5=good.x(w-3.5u); rt x2r=hround max(rt x3,w-u)+eps; fi
lft x4r=hround max(.5w+.5stem+.5,w-6.5u-.5curve+eps); x6r=good.x(w+eps); x0=x6;
y2=.5[bar_height,h]; top y3r=h+oo; bulb(3,2,1);  % bulb
bot y5r=-oo; y4=.5[y3,y5]; top y6l=vround .5bar_height; y0=bar_height;
path p; p=z5{right}..z6..z0;
filldraw stroke pulled_arc.e(3,4)
 & pulled_arc.e(4,5)...{direction 1 of p}z6e;  % right arc
pos12(flare,180); pos11(hair,180); pos10(vair,270);
pos9(curve,360); pos8(vair,450); pos7(hair,500);
if monospace: lft x11r=good.x (-eps); x10=x8=.5[lft x11r,rt x9r]+eps;
else: x10=x8=3.5u; lft x11r=hround min(lft x10,u)-eps; fi
rt x9r=hround min(.5w-.5stem-.5,6.5u+.5curve-eps);
x7r=good.x (if not monospace:rt x11r-.67u fi-eps); x0'=x7;
y11=h-.5[bar_height,h]; bot y10r=-oo; bulb(10,11,12);  % bulb
top y8r=h+oo; y9=.5[y10,y8]; bot y7l=vround (h-.65bar_height); y0'=bar_height;
path p_; p_=z8{left}..z7..z0;
filldraw stroke pulled_arc.e(10,9)
 & pulled_arc.e(9,8)...{direction 1 of p_}z7e;  % left arc
y13=bar_height; x13=x4; y14=y13; x14=w-x4;  x15=x16=.5w;
y15+.5stem=h+oo; bot y16=0;
pos13(vair,90); pos14(vair,90);  pos15(fudged.stem,0); pos16(fudged.stem,0);
filldraw stroke z13e--z14e; filldraw circ_stroke z15e--z16e; % stem & cross
%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 17,18,19,20);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16, 17,18,19,20,21); endchar;

lhchar "Italic Cyrillic letter z_uml - ze umlaut";
cyrchar(z_uml,8u#,lc_trema_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi; %e
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_z;
pickup fine.nib; pos0(flare,180); pos1(hair,180); pos2(vair,-90);
pos7(curve,0); pos8(curve,0); pos4(vair,90); pos5(hair,160);
pos3(vair,90); pos3'(vair,-90); pos9(vair,90); pos9'(vair,-90);
x2=x4=.5(w-u); lft x1r=min(lft x2,hround(.25u))-eps;
lft x5r=good.x x1r+.15u+eps; x6=x5;
y1=.5bar_height; bot y2r=-oo; bulb(2,1,0);  % bulb
rt x7r=w-hround(1.5u-.5curve); rt x8r=w-hround(2u-.5curve);
top y4r=h+oo; y7=.5[y2,y3]; y5r=y8=.5[y3,y4]; y6=bar_height;
x3=x2; lft x9=hround min(lft x2-1-eps,rt x0l+1+eps);
y3=y9=bar_height; z9'=z9; z3'=z3;
path p; p=z4{left}..(x5,h-.52bar_height)..z6;
filldraw stroke z9e{right}...pulled_arc.e(3,7) & pulled_arc.e(7,2); % lower arc
filldraw stroke z9'e{right}...pulled_arc.e(3',8) & pulled_arc.e(8,4)
  ...{direction 1 of p}z5e;  % upper arc
%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 10,11,12,13);
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

lhchar "Italic Cyrillic letter i_bar - i bar (macron)";
cyrchar(i_bar,10u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_i;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left skewstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
% the accent
h:=full_h;
% low_bar(.5w,0, 10,11);
x_move:=.5w; y_move:=0; _one:=10; _two:=11;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11); endchar;

lhchar "Italic Cyrillic letter i_uml - i umlaut";
cyrchar(i_uml,10u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); h:=x_height;
% ital_i;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left skewstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
% the accent
lowercase_umlaut(0,0, 10,11,12,13);
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

lhchar "Italic Cyrillic letter o_uml - o umlaut";
cyrchar(o_uml,9u#,lc_trema_height#,0);
italcorr .7asc_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); h:=x_height; pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
% the accent
lowercase_umlaut(0,0, 5,6,7,8);
penlabels(1,2,3,4, 5,6,7,8); endchar;

lhchar "Italic Cyrillic letter otld_uml - o tilded umlaut";
cyrchar(otld_uml,9u#,lc_trema_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); full_h:=h; h:=x_height; pickup fine.nib;
% ital_o;tld
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
%%%
% oltilde;
if (serifs=true) and (bar#<=.5curve#):
   numeric bar[];
   set_bar_axis;
   x5r=x2l; x6r=x4l;
   y5r=y6r=y2.bh; y5l=y6l=y1.bh; x5r=x5l; x6l=x6r;
   bar2=2/3[bar,fudged.stem];
   penpos7(bar2,45); z7=.5[z5l,z6l];
   fill stroke z6e{-2,-1}..{-2,1}z7e..{-2,-1}z5e;
else:
   bar_stroke(x2l,x4l);
fi
%%%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 8,9,10,11);
penlabels(1,2,3,4,5,6,7, 8,9,10,11); endchar;

lhchar "Italic Cyrillic letter ukre_uml - Ukrainian e (c with mid-line)";
cyrchar(ukre_uml,8u#,lc_trema_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-.5u# fi; %c+.5u
adjust_fit(if monospace:0 else:-.35u#fi,0); %c
 full_h:=h; h:=x_height;
% ital_ukre;
pickup fine.nib; pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-.5u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
pos7(vair,90); pos8(vair,90);
y7=bar_height; rt x7=hround max(rt x2+1+eps,lft x0l-eps); x8=x3; y7=y8;
filldraw stroke z7e--z8e;
%%%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 9,10,11,12);
penlabels(1,2,3,4,5,6,7,8); endchar;

lhchar "Italic Cyrillic letter erev_uml - reverse e umlaut";
cyrchar(erev_uml,8u#,lc_trema_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi; %o
adjust_fit(0,if monospace:0 else:-.35u# fi); %o
h:=x_height;
% ital_erev;
pickup fine.nib;
pos0(flare,180); pos1(hair,-180); pos2(vair,-90);
pos3(curve,0); pos4(vair,90); pos5(hair,140);
x2+.5u=x4=.5(w-u); lft x1r=min(lft x2,hround(.25u))-eps;
rt x3r=w-hround(1.5u-.5curve); lft x5r=good.x (lft x1r-eps); x6=x5;
y1-.5flare=.1h; bot y2r=-oo; bulb(2,1,0);  % bulb
top y4r=h+oo; y3=.5[y2,y4]; top y5r=vround .5[bar_height,h];
y6=bar_height; path p; p=z4{left}....z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x7=x3; y7=bar_height; y8=y7; lft x8=hround min(lft x2-1-eps,rt x0l+eps);
pos7(vair,90); pos8(vair,90);
filldraw stroke z7e--z8e;
%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 9,10,11,12);
penlabels(0,1,2,3,4,5,6,7,8, 9,10,11,12); endchar;

lhchar "Italic Cyrillic letter u_bar - u bar (macron)";
cyrchar(u_bar,if monospace:8.5u# else:9u#fi,lc_trema_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
h:=full_h;
% low_bar(.5w,0, 11,12);
x_move:=.5w; y_move:=0; _one:=11; _two:=12;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12); endchar;

lhchar "Italic Cyrillic letter u_uml - u umlaut";
cyrchar(u_uml,if monospace:8.5u# else:9u#fi,lc_trema_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
h:=full_h; lowercase_umlaut(0,0, 11,12,13,14);
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12,13,14); endchar;

lhchar "Italic Cyrillic letter u_dac - u double acute";
cyrchar_twice(u_dac,if monospace:8.5u# else:9u#fi,x_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0);
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
if monospace: hpos_corr=-2u; put_accent; hpos_corr=2u; put_accent;
else: hpos_corr=-1.5u; put_accent; hpos_corr=1.5u; put_accent; fi
repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

lhchar "Italic Cyrillic letter ch_uml - che umlaut";
cyrchar(ch_uml,9.5u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_ch;
pickup fine.nib; interim superness:=more_super;
x2=x3; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace:
 pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h; pos2(stem,0);
 filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else:
 x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
bot y4r=.75bar_height; y5=.75h; y3=min(y2-eps,.7[y4,y5]); y6+.5stem=h;
filldraw stroke z2'e...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
%%%
% the accent
h:=full_h; lowercase_umlaut(0,0, 10,11,12,13);
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter ery_uml - ery umlaut";
cyrchar(ery_uml,8u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:-2u#,2.5u# else:0,4u# fi);
h:=x_height;
% ital_ery;
pickup tiny.nib; pos2(stem,0); y2=.6bar_height;
if monospace: x1=x2; pos1(stem,0); lft x1l=hround(2.5u-.5stem); top y1=h;
else: x1=x2+.25u; x0'=0; lft x2l=hround(2.5u-.5stem);
 hook_in(0',a,1)(skewed); fi  % opening hook
pickup fine.nib; pos1'(stem,-180); pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z1'=z1; z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke z1'e{-u,-x_height}...pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
% I
w:=w+4u;
pos6(stem,0); y6+.5stem=h; rt x6r=hround(w-2.5u+.5stem); x7=x6;
if monospace: pos9(vair,90); x9=good.x .5[x6,w]; bot y9l=0; y7=1/3h;
 pos7(stem,0); filldraw stroke z7e{down}...{right}z9e; % terminal
else: x9=w; hook_out(7,8,9); fi % opening hook
filldraw circ_stroke z6e--z7e; % stem
% the accent
lowercase_umlaut(0,0, 11,12,13,14);
penlabels(0,1,1',2,3,4,5,6,7,8,9,10, 11,12,13,14); endchar;

endinput;
%end of file
