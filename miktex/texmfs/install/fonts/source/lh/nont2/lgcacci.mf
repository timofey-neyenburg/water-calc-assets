%
% lgcacci.mf
%
%% Cyrillic font container with T2 encoding beta-support
%
% This file is future part of lxfonts package
% Version 3.5 // Patchlevel=1
% (c) O.Lapko
%
% This package is freeware product under conditions similar to
% those of D. E. Knuth specified for the Computer Modern family of fonts.
% In particular, only the authors are entitled to modify this file
% (and all this package as well) and to save it under the same name.
%
% Content:
%
% Italic Cyrillic accented letters
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% List of letternames
%
% a_bar
% o_bar
% o_lcom
% u_tld
% u_hat
% u_gr
% u_com
% ery_bar
% erev_dot
% erev_bar
% yu_bar
% ya_bar
% otld_bar
% y_bar
% Vy (italic variant)
% Vy_bar (italic variant)
% eps_uml
%
% l_ldot
% n_ldot
% r_brv
% t_lcom
% ch_ldot
%
% Vg - Serbian italic variant
% Vp - Serbian italic variant
% Vt - Serbian italic variant
%

LHver_check(3,5); % like |version_check| in ec

lhchar "Italic Cyrillic letter a_bar";
cyrchar(a_bar,9u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:0 else:-.35u#fi,0); %Cyrillic adj
full_h:=h; h:=x_height;
% ital_a;
pickup fine.nib;
pos0(hair,0); pos1(vair,90); pos2(curve,180); pos3(vair,270);
pos4(stem,0); x4=x0; y4+.5stem=vround .98h;
x1=x3=.5[x0,x2]; lft x2r=hround(1.5u-.5curve);
rt x4r=hround(w-2.5u+.5stem);
top y1r=h+oo; bot y3r=-oo; y0=y2=.5[y1,y3];
filldraw stroke super_arc.e(0,1) & pulled_arc.e(1,2)
 & pulled_arc.e(2,3) & super_arc.e(3,0);  % bowl
x5=x4; x7=w; hook_out(5,6,7);  % closing hook
filldraw circ_stroke z4e--z5e;  % stem
% the accent
h:=full_h;
% low_bar(.5w,0, 8,9);
x_move:=.5w; y_move:=0; _one:=8; _two:=9;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7, 8,9); endchar;

lhchar "Italic Cyrillic letter o_bar";
cyrchar(o_bar,9u#,lc_trema_height#,0);
italcorr .7asc_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); full_h:=h; h:=x_height; pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
% the accent
h:=full_h;
% low_bar(x1,0, 5,6);
x_move:=x1; y_move:=0; _one:=5; _two:=6;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(1,2,3,4, 5,6); endchar;

lhchar "Italic Cyrillic letter o_lcom - o lower comma";
cyrchar(o_lcom,9u#,x_height#,1.25desc_depth#);
italcorr .7asc_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
% the accent
%ac_comma(0,-oo,1.25desc_depth, 5,6,7,8,9);
 x_move:=0; y_move:=-oo; depth:=1.25desc_depth;
  _zero:=5; _one:=6; _two:=7; _three:=8; _four:=9;
 numeric height,bulb_diam;
 height=if serifs: depth+.5fine-max(.27depth,o+hair) else:.68depth fi;
 bulb_diam=max(fine.breadth,min(dot_size,floor(.56height)));
 if square_dots: pickup fine.nib; pos[_one](vair,0);
  pos[_two](.5[vair,stem],0);
  % comma below letter as sans serif cedilla
   top y[_one]=-o-2; bot y[_two]=-d-o;
   x[_two]=x[_one]-1.25u=.5w+x_move;
  filldraw stroke z[_one]e--z[_two]e;   % diagonal
 else: pickup fine.nib;
  x[_four]-.5bulb_diam=hround(.5w+x_move-.5bulb_diam);
  y[_four]+.5bulb_diam=-d+y_move+height;
  pos[_four](bulb_diam,90);
  pos[_one](vair,90); pos[_two](vair,0); pos[_three](vair,-45);
  z[_one]r=z[_four]r; 
  rt x[_two]r=hround max(rt x[_one]+eps,x[_four]+.5bulb_diam
   +if monospace:.28u / expansion_factor else:.25u fi)+2eps;
  x[_three]=x[_four]-.5u if monospace: /expansion_factor fi;
  y[_two]=1/3[y[_one],y[_three]]+eps;
  bot y[_three]r=vround min(bot y[_four]-.5,y[_four]-(height-.5bulb_diam))-eps;
  y_:=ypart((z[_one]{right}...z[_two]{down}...z[_three])
   intersectiontimes (z[_four]l{right}..{left}z[_four]r));
  if y_<0: y_:=1; fi
  filldraw z[_four]r{left}..subpath (0,y_)
   of (z[_four]l{right}..{left}z[_four]r)--cycle; % dot
  filldraw stroke z[_one]e{right}...z[_two]e{down}...z[_three]e; fi  % tail
 penlabels([_zero],[_one],[_two],[_three],[_four]);
penlabels(1,2,3,4, 5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter u_tld - u tilde";
cyrchar(u_tld,if monospace:8.5u# else:9u#fi,x_height#+acc_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
h:=full_h; lowercase_tilde(0,0, 11,12,13,14,15);
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12,13,14,15); endchar;

lhchar "Italic Cyrillic letter u_hat - u hat";
cyrchar(u_hat,if monospace:8.5u# else:9u#fi,x_height#+acc_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
h:=full_h; lowercase_hat(.5[if monospace: x2 else: x1 fi,x6],0, 11,12,13,14,15);
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12,13,14,15); endchar;

lhchar "Italic Cyrillic letter u_gr - u grave";
cyrchar_twice(u_gr,if monospace:8.5u# else:9u# fi,x_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0);
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
put_gravis; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

lhchar "Italic Cyrillic letter u_com";
cyrchar(u_com,if monospace:8.5u# else:9u#fi,x_height#+acc_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_u;
pickup fine.nib; interim superness:=more_super;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
% the accent
h:=full_h;
%ac_comma(0,0,1.25acc_height, 11,12,13,14,15)(above);
 x_move:=0; y_move:=0; depth:=1.25acc_height;
  _zero:=11; _one:=12; _two:=13; _three:=14; _four:=15;
 numeric height,bulb_diam;
 height=if serifs:
  depth+.5fine-max(.27depth,o+hair) else:.68depth fi;
 bulb_diam=max(fine.breadth,min(dot_size,floor(.56height)));
 if square_dots: pickup fine.nib; pos[_one](vair,0);
  pos[_two](.5[vair,stem],0);
  %comma above letter: vertical
   top y[_two]=h+y_move;
   bot y[_one]=vround(y[_two]-height);
   x[_two]=x[_one]=.5w+x_move;
  filldraw stroke z[_one]e--z[_two]e;   % diagonal
 else: pickup fine.nib;
  x[_four]-.5bulb_diam=hround(.5w+x_move-.5bulb_diam);
  y[_four]+.5bulb_diam=h+y_move;
  pos[_four](bulb_diam,90);
  pos[_one](vair,90); pos[_two](vair,0); pos[_three](vair,-45);
  z[_one]r=z[_four]r; 
  rt x[_two]r=hround max(rt x[_one]+eps,x[_four]+.5bulb_diam
   +if monospace:.28u / expansion_factor else:.25u fi)+2eps;
  x[_three]=x[_four]-.5u if monospace: /expansion_factor fi;
  y[_two]=1/3[y[_one],y[_three]]+eps;
  bot y[_three]r=vround min(bot y[_four]-.5,y[_four]-(height-.5bulb_diam))-eps;
  y_:=ypart((z[_one]{right}...z[_two]{down}...z[_three])
   intersectiontimes (z[_four]l{right}..{left}z[_four]r));
  if y_<0: y_:=1; fi
  filldraw z[_four]r{left}..subpath (0,y_)
   of (z[_four]l{right}..{left}z[_four]r)--cycle; % dot
  filldraw stroke z[_one]e{right}...z[_two]e{down}...z[_three]e; fi  % tail
 penlabels([_zero],[_one],[_two],[_three],[_four]);
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12,13,14,15); endchar;

lhchar "Italic Cyrillic letter ery_bar";
cyrchar(ery_bar,8u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#; %u
adjust_fit(if monospace:-2u#,2.5u# else:0,4u# fi); h:=x_height;
% ital_ery;
pickup tiny.nib; pos2(stem,0); y2=.6bar_height;
if monospace: x1=x2; pos1(stem,0); lft x1l=hround(2.5u-.5stem); top y1=h;
else: x1=x2+.25u; x0'=0; lft x2l=hround(2.5u-.5stem);
 hook_in(0',a,1)(skewed); fi  % opening hook
pickup fine.nib; pos1'(stem,-180); pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z1'=z1; z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke z1'e{-u,-x_height}...pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
% I
w:=w+4u;
pos6(stem,0); y6+.5stem=h; rt x6r=hround(w-2.5u+.5stem); x7=x6;
if monospace: pos9(vair,90); x9=good.x .5[x6,w]; bot y9l=0; y7=1/3h;
 pos7(stem,0); filldraw stroke z7e{down}...{right}z9e; % terminal
else: x9=w; hook_out(7,8,9); fi % opening hook
filldraw circ_stroke z6e--z7e; % stem
% the accent
h:=full_h;
if monospace:
%low_bar(.5[l,r],oo, 11,12);
 x_move:=.5[l,r]; y_move:=oo; _one:=11; _two:=12;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
 filldraw stroke z[_one]e--z[_two]e;  % bar
else:
% low_wbar(x1,w-x1,oo, 11,12);
 x_left:=x1; x_right:=w-x1; y_move:=oo; _one:=11; _two:=12;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_left; x[_two]=x_right;
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 filldraw stroke z[_one]e--z[_two]e;  fi % bar
penlabels(0,1,1',2,3,4,5,6,7,8,9,10, 11,12); endchar;

lhchar "Italic Cyrillic letter erev_dot - reverse e dot";
cyrchar_twice(erev_dot,8u#,x_height#,0);
italcorr x_height#*slant-beak_jut#-.25u#; %o
adjust_fit(0,if monospace:0 else:-.35u# fi); %o
begingroup
% ital_erev;
pickup fine.nib;
pos0(flare,180); pos1(hair,-180); pos2(vair,-90);
pos3(curve,0); pos4(vair,90); pos5(hair,140);
x2+.5u=x4=.5(w-u); lft x1r=min(lft x2,hround(.25u))-eps;
rt x3r=w-hround(1.5u-.5curve); lft x5r=good.x (lft x1r-eps); x6=x5;
y1-.5flare=.1h; bot y2r=-oo; bulb(2,1,0);  % bulb
top y4r=h+oo; y3=.5[y2,y4]; top y5r=vround .5[bar_height,h];
y6=bar_height; path p; p=z4{left}....z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x7=x3; y7=bar_height; y8=y7; lft x8=hround min(lft x2-1-eps,rt x0l+eps);
pos7(vair,90); pos8(vair,90);
filldraw stroke z7e--z8e;
%%%
% the accent
endgroup; put_dot(fine,flare#); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8); endchar;

lhchar "Italic Cyrillic letter erev_bar - reverse e bar";
cyrchar(erev_bar,8u#,lc_trema_height#,0);
italcorr x_height#*slant-beak_jut#-.25u#; %o
adjust_fit(0,if monospace:0 else:-.35u# fi); %o
full_h:=h; h:=x_height;
% ital_erev;
pickup fine.nib;
pos0(flare,180); pos1(hair,-180); pos2(vair,-90);
pos3(curve,0); pos4(vair,90); pos5(hair,140);
x2+.5u=x4=.5(w-u); lft x1r=min(lft x2,hround(.25u))-eps;
rt x3r=w-hround(1.5u-.5curve); lft x5r=good.x (lft x1r-eps); x6=x5;
y1-.5flare=.1h; bot y2r=-oo; bulb(2,1,0);  % bulb
top y4r=h+oo; y3=.5[y2,y4]; top y5r=vround .5[bar_height,h];
y6=bar_height; path p; p=z4{left}....z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x7=x3; y7=bar_height; y8=y7; lft x8=hround min(lft x2-1-eps,rt x0l+eps);
pos7(vair,90); pos8(vair,90);
filldraw stroke z7e--z8e;
%%%
% the accent
h:=full_h;
% low_bar(.5w,oo, 9,10);
x_move:=.5w; y_move:=oo; _one:=9; _two:=10;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8, 9,10); endchar;

lhchar "Italic Cyrillic letter yu_bar (looks like io)";
cyrchar(yu_bar,9u#,lc_trema_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(if monospace:3u# else:4.5u# fi,
 if monospace:-.75u# else:-.35u# fi);
full_h:=h; h:=x_height;
% ital_yu;
pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
% i half
_one:=5; _two:=6; _three:=7; _four:=8; _five:=9; _six:=10;
%
pos[_four](stem,0);
lft x[_four]l=l+letter_fit if not monospace: +hround(2.5u-.5stem) fi;
y[_four]-.5stem=-oo; x[_three]=x[_four];
if monospace:
 pos[_three](stem,0); top y[_three]=h;
else:
 x[_one]=l+letter_fit; hook_in([_one],[_two],[_three]); fi % opening hook
filldraw circ_stroke z[_four]e--z[_three]e; % stem
pos[_five](vair,90); pos[_six](vair,90); %!!!
x[_five]=x[_four]; x[_six]=x2; y[_five]=y[_six]=bar_height; %!!!
filldraw stroke z[_five]e--z[_six]e; % bar %!!!
%%%
% the accent
h:=full_h;
if monospace:
% low_bar(.5[l,r],oo, 11,12);
 x_move:=.5[l,r]; y_move:=oo; _one:=11; _two:=12;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
 filldraw stroke z[_one]e--z[_two]e;  % bar
else:
% low_wbar(x9,w-(x9-l),oo, 11,12);
 x_left:=x9; x_right:=w-(x9-l); y_move:=oo; _one:=11; _two:=12;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_left; x[_two]=x_right;
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 filldraw stroke z[_one]e--z[_two]e;  fi % bar
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12); endchar;

lhchar "Italic Cyrillic letter ya_bar (looks like small backward R)";
cyrchar(ya_bar,9u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
% ital_ya;
pickup fine.nib;
rt x1r=rt x2r=hround(w-2.5u+.5stem); top y1=h; x4=w;
hook_out(2,3,4);  % closing hook
pos1(stem,0); filldraw stroke z1e--z2e;  % stem
pos5(vair,90); pos6(vair,90); pos7(curve,180); pos8(vair,270); pos9(vair,270);
x5=x9=x1; top y5r=top y6r=h; x6=x8=.5[x5,x7];
lft x7l=hround(1.5u if not monospace:+.5curve fi);
y7=.5[y5,y9]; y8=y9=bar_height;
filldraw stroke z5e...pulled_arc.e(6,7) & pulled_arc.e(7,8)...z9e; % bowl
pos10(stem,0); pos11(vair,-90);
x10=.3[x11,x9]; y10=1/2y9; x11-flare=-.25u; bot y11r=-oo;
filldraw stroke z11e{right}...z10e{up}...{right}z9e;  % lower diagonal
pos12(hair,180); pos13(flare,180);
lft x13r=-.25u; y13-.5flare=.1h; bulb(11,12,13); % bulb
%%%
% the accent
h:=full_h;
% low_bar(.5w,0, 14,15);
x_move:=.5w; y_move:=0; _one:=14; _two:=15;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(1,2,3,4,5,6,7,8,8',9,10,11,12,13, 14,15); endchar;

lhchar "Italic Cyrillic letter otld_bar (otld bar)";
cyrchar(otld_bar,9u#,lc_trema_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); full_h:=h; h:=x_height; pickup fine.nib;
% ital_o;tld
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
%%%
% oltilde;
if (serifs=true) and (bar#<=.5curve#):
   numeric bar[];
   set_bar_axis;
   x5r=x2l; x6r=x4l;
   y5r=y6r=y2.bh; y5l=y6l=y1.bh; x5r=x5l; x6l=x6r;
   bar2=2/3[bar,fudged.stem];
   penpos7(bar2,45); z7=.5[z5l,z6l];
   fill stroke z6e{-2,-1}..{-2,1}z7e..{-2,-1}z5e;
else:
   bar_stroke(x2l,x4l);
fi
%%%%%
% the accent
h:=full_h;
% low_bar(.5w,oo, 8,9);
x_move:=.5w; y_move:=oo; _one:=8; _two:=9;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(1,2,3,4,6,7, 8,9); endchar;

lhchar "Italic Cyrillic letter y_bar - straight u bar";
cyrchar(y_bar,9.5u#,lc_trema_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(.25u#,max(u#,.5u#+.5flare#)); %izhy_adj
full_h:=h; h:=x_height;
% ital_y;
numeric left_stem,right_stem,outer_jut,dy,alpha; left_stem=stem-stem_corr;
right_stem=min(hair if hefty:-2stem_corr fi,left_stem); outer_jut=.75jut;
x1l=l+letter_fit+outer_jut+.25u; x4r=w-x1l;
x2l=x5l=hround(.5w-.5left_stem); x3r=x5r; y1=y4=h;
y2=y3=y5=0; dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0);
penpos4(alpha*right_stem,0); penpos4'(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0);
x5=x6; y6=-d; z0=whatever[z1r,z2r]=whatever[z3l,z4l];
penpos7(vair,90); penpos8(hair,0); penpos9(flare,0);
x7=.5[x4',rt x8r]; y7r=h+oo;
y4'=min(.9x_height,y7l-vair); z4'=whatever[z3,z4];
rt x8r=hround(r-.5u-.5);
y8=max(vround(y7r-.5hair)-.5flare-1,
 min(bar_height+.5flare+2vair'+2,.9[bar_height,h]-.5flare));
cyrbulb(7,8,9); % bulb
y1'=3/4x_height; z1'=whatever[z1,z2]; penpos1'(alpha*left_stem,0);
x10=l+letter_fit; x10:=hround(x10-.5hair)+.5hair; y10=2/3x_height;
penpos11(vair,90); top y11r=x_height+oo; x11=min(x1l-.5,x10+u);
if y0>y2+notch_cut:
 y0:=y2+notch_cut;
 fill z0+.5right{up}...{z4-z3}z4'l...{right}z7r--z7l{left}
  ...{z5r-z4'r}z4'r--z5r--z6r--z6l--z5l--
   z1'l{z1-z2}...{left}z11l--z11r{right}
   ..tension atleast .75 and 1..{z2-z1}z1'r
  ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--z4'l...{right}z7r--z7l{left}
  ...z4'r--z5r--z6r--z6l--z5l--
   z1'l{z1-z2}...{left}z11l--z11r{right}
   ..tension atleast .75 and 1..{z2-z1}z1'r
  --cycle; fi  % diagonals and stem
pickup fine.nib;
pos10(hair,180); pos11'(vair,90); z11'=z11;
filldraw stroke z10e{up}...z11'e{right};  % hook
pickup tiny.nib; prime_points_inside(6,5);
dish_serif(6',5,e,1/3,jut,f,1/3,jut); % lower serif
%%%
% the accent
h:=full_h;
% low_bar(.5w,0, 13,14);
x_move:=.5w; y_move:=0; _one:=13; _two:=14;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12, 13,14); endchar;

% italic variants of straight u (like flipped h)
lhchar "Italic Cyrillic letter Vy - variant straight u (flipped h)";  %
cyrchar(Vy,if monospace:8.5u# else:9u#fi,x_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0);
pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; x5=x6=x7;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
pickup tiny.nib; y6+.5stem=h; z6'=z6;
pos6'(stem,0); pos7(stem,0);
bot y7=-d; filldraw circ_stroke z6e--z7e;  % stem
dish_serif(7,6,a,1/3,jut,b,1/3,.75jut);	% serif
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

lhchar "Italic Cyrillic letter Vy_bar - variant straight u macron";  %
cyrchar(Vy_bar,if monospace:8.5u# else:9u#fi,lc_trema_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; x5=x6=x7;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
pickup tiny.nib; y6+.5stem=h; z6'=z6;
pos6'(stem,0); pos7(stem,0);
bot y7=-d; filldraw circ_stroke z6e--z7e;  % stem
dish_serif(7,6,a,1/3,jut,b,1/3,.75jut);	% serif
% the accent
h:=full_h;
% low_bar(.5w,0, 11,12);
x_move:=.5w; y_move:=0; _one:=11; _two:=12;
%
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8,9,10, 11,12); endchar;

lhchar "Italic Cyrillic letter Vy_hcrs - variant cross straight u";
cyrchar(Vy_hcrs,if monospace:8.5u# else:9u#fi,x_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2; pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; x5=x6=x7;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
pickup tiny.nib; y6+.5stem=h; z6'=z6;
pos6'(stem,0); pos7(stem,0);
bot y7=-d; filldraw circ_stroke z6e--z7e; % stem
dish_serif(7,6,a,1/3,jut,b,1/3,.75jut);	% serif
% bar
pickup fine.nib; numeric middle_weight;
middle_weight=max(fine.breadth,.6[thin_join,vair]);
y10=y11=hround min(bot y4-1,.5[bot y4,-d+vair'])-.5middle_weight;
x11=lft x7l-jut; x10=rt x7r+.75jut; pos11(middle_weight,90); pos10(middle_weight,90);
filldraw stroke z10e--z11e;
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11); endchar;

lhchar "Italic Cyrillic letter eps_uml - epsilon umlaut";
cyrchar(eps_uml,if serifs:8u# else:7.5u# fi,lc_trema_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
h:=x_height;
% ital_eps;
pickup fine.nib; pos0(flare,0); pos1(hair,0); pos2(vair,90); pos7(curve,180);
pos9(vair,270); pos3(vair,270); pos9'(vair,90); pos3'(vair,90);
pos8(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-.25u))+eps;
x5r=good.x w-eps; x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
lft x7r=hround(2u-.5curve); lft x8r=hround(1.5u-.5curve);
bot y4r=-oo; y7=.5[y2,y3]; y5r=y8=.5[y3,y4]; y6=bar_height;
x3=x2; rt x9=hround max(rt x2+1+eps,lft x0l-eps); y3=y9=bar_height;
z9'=z9; z3'=z3;
path p; p=z4{left}..(x5,15/16bar_height)..z6;
filldraw stroke z9e{left}...super_arc.e(3,7) & super_arc.e(7,2); % lower arc
filldraw stroke z9'e{left}...pulled_arc.e(3',8) & pulled_arc.e(8,4)
  ...{direction 1 of p}z5e;  % upper arc
%%%
% the accent
lowercase_umlaut(0,0, 10,11,12,13);
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

lhchar "Italic Cyrillic letter l_ldot - el lower dot";
cyrchar(l_ldot,9u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(.5u#,0);
% ital_l;
pickup fine.nib;
pos1(vair,-90); pos2(stem,180); pos3(hair,0); pos10(stem,0);
pos4(vair,-90); pos5(hair,-180); pos6(flare,-180);
top y1l=h+oo; x1=.5[x3,x2];
x3=3u; y3=.35h;
y6-.5flare=.1h; lft x6r=-.25u; z5r=z6r;
bot y4r=-oo; x4=max(.4[lft x6r,rt x3r],lft x6r+.5flare-eps);
y10=.8h; lft x10l=lft x7l=hround(w-2.5u-.5stem); z10=z2;
bulb(4,5,6); % bulb
filldraw stroke pulled_arc.e(4,3)
 & super_arc.e(3,1) & super_arc.e(1,2); % left stem
x9=w; hook_out(7,8,9);  % hook
filldraw stroke z10e--z7e;  % right stem
%%%
% the accent
% lower_dot;
_one:=11; _two:=12; _center:=.5w;
%
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
define_whole_blacker_pixels(dot_diam);
pos[_one](dot_diam,0); pos[_two](dot_diam,90);
x[_one]=hround(_center); bot y[_two]l=vround(-d-1);
if 0-oo-top y[_two]r<slab: y[_one]r:=vround(max(y[_two]l+eps,-fine-slab)); fi
x[_one]=x[_two]; y[_one]=vround(.5[y[_two]l,y[_two]r]);
dot([_one],[_two]);  % dot
penlabels(1,1',2,3,4,5,6,7,8,9,10, 11,12); endchar;

lhchar "Italic Cyrillic letter n_ldot - en lower dot";
cyrchar(n_ldot,10u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0);
% ital_n;
pickup fine.nib; x2=x3; pos3(stem,0); lft x3l=hround(2.5u-.5stem);
%ihalfstem;
if monospace: pos2(stem,0); top y2=h;
else: x0=0; hook_in(0,1,2); fi % opening hook
y3-.5stem=-oo;
filldraw circ_stroke z3e--z2e;
x10=x3l; y10=y11=bar_height; pos10(vair,90); pos11(vair,90);
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem); y6+.5stem=h;
x7=x6; x9=w; hook_out(7,8,9);  z7'=z7; pos7'(stem,0);
filldraw circ_stroke z6e--z7'e; x11=x6l; filldraw stroke z10e--z11e;
%%%
% the accent
% lower_dot;
_one:=12; _two:=13; _center:=.5w;
%
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
define_whole_blacker_pixels(dot_diam);
pos[_one](dot_diam,0); pos[_two](dot_diam,90);
x[_one]=hround(_center); bot y[_two]l=vround(-d-1);
if 0-oo-top y[_two]r<slab: y[_one]r:=vround(max(y[_two]l+eps,-fine-slab)); fi
x[_one]=x[_two]; y[_one]=vround(.5[y[_two]l,y[_two]r]);
dot([_one],[_two]);  % dot
penlabels(0,1,2,3,4,5,6,7,8,9,10,11, 12,13); endchar;

lhchar "Italic Cyrillic letter r_brv - r breve";
cyrchar(r_brv,if monospace:9u#else:9.25u#fi,x_height#+acc_height#,desc_depth#); % the Russian width
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi; %p
adjust_fit(0,if monospace:0 else:-.35u#fi); full_h:=h; h:=x_height;
% ital_r;
pickup fine.nib;
x0=0; x2-.5stem=hround(2.5u-.5stem); hook_in(0,1,2);  % opening hook
pos4(hair,-180); pos5(vair,-90); pos6(curve,0); pos7(vair,90);
x4=x2; rt x6r=hround(w-1.5u+.5curve); x5=x7=.5[x4,x6];
bot y5r=-oo; top y7r=h+oo; y4=y6=.5[y5,y7];
filldraw stroke super_arc.e(4,5) & pulled_arc.e(5,6)
 & pulled_arc.e(6,7) & super_arc.e(7,4);  % bowl
pickup tiny.nib; pos2'(stem,0); pos3(stem,0);
z2=z2'; x3=x2; bot y3=-d; filldraw stroke z2'e--z3e;  % stem
dish_serif(3,2',a,1/3,.75jut,b,1/3,jut);  % serif
%%%
% the accent
h:=full_h; lowercase_breve(.5w,0, 8,9,10); %x3 y3r-h
penlabels(0,1,2,3,4,5,6,7, 8,9,10); endchar;

lhchar "Italic Cyrillic letter t_lcom - te lower comma";
cyrchar(t_lcom,15u#,x_height#,1.25desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:-1.5u#,-2u# else: 0,0 fi);
% ital_t;
pickup fine.nib; numeric shaved_stem; shaved_stem=mfudged.stem;
save stem; stem=shaved_stem; pos2(stem,0); x1=x2;
if monospace: pos1(stem,0); lft x1l=hround(2.5u-.5stem); top y1=h;
else: x0=0; lft x2l=hround(2.5u-.5stem);
 hook_in(0,a,1); fi  % opening hook
y2-.5stem=-oo; filldraw circ_stroke z2e--z1e;  % left stem
x4+.5stem=hround(.5w+.5stem); ital_arch(2,3,4);  % left arch
pos5(stem,0); y5=y2; x5=x4;
filldraw circ_stroke z5e--z4e;  % middle stem
x7+.5stem=hround(w-2.5u+.5stem);
x8=x7-.25u; ital_arch(5,6,7);  % right arch
if monospace: pos9(vair,90); x9=good.x .5[x8,w]; bot y9l=0; y8=1/3h;
 pos8(stem,0); filldraw stroke z8e{-u,-x_height}...{right}z9e; % terminal
else: x9=w; hook_out(8,b,9)(skewed); fi  % closing hook
filldraw stroke z7e{down}..{-u,-x_height}z8e;  % right stem
% the accent
%ac_comma(0,0,1.25desc_depth, 10,11,12,13,14);
 x_move:=0; y_move:=0; depth:=1.25desc_depth;
  _zero:=10; _one:=11; _two:=12; _three:=13; _four:=14;
 numeric height,bulb_diam;
 height=if serifs: depth+.5fine-max(.27depth,o+hair) else:.68depth fi;
 bulb_diam=max(fine.breadth,min(dot_size,floor(.56height)));
 if square_dots: pickup fine.nib; pos[_one](vair,0);
  pos[_two](.5[vair,stem],0);
  % comma below letter as sans serif cedilla
   top y[_one]=-o-2; bot y[_two]=-d-o;
   x[_two]=x[_one]-1.25u=.5w+x_move;
  filldraw stroke z[_one]e--z[_two]e;   % diagonal
 else: pickup fine.nib;
  x[_four]-.5bulb_diam=hround(.5w+x_move-.5bulb_diam);
  y[_four]+.5bulb_diam=-d+y_move+height;
  pos[_four](bulb_diam,90);
  pos[_one](vair,90); pos[_two](vair,0); pos[_three](vair,-45);
  z[_one]r=z[_four]r; 
  rt x[_two]r=hround max(rt x[_one]+eps,x[_four]+.5bulb_diam
   +if monospace:.28u / expansion_factor else:.25u fi)+2eps;
  x[_three]=x[_four]-.5u if monospace: /expansion_factor fi;
  y[_two]=1/3[y[_one],y[_three]]+eps;
  bot y[_three]r=vround min(bot y[_four]-.5,y[_four]-(height-.5bulb_diam))-eps;
  y_:=ypart((z[_one]{right}...z[_two]{down}...z[_three])
   intersectiontimes (z[_four]l{right}..{left}z[_four]r));
  if y_<0: y_:=1; fi
  filldraw z[_four]r{left}..subpath (0,y_)
   of (z[_four]l{right}..{left}z[_four]r)--cycle; % dot
  filldraw stroke z[_one]e{right}...z[_two]e{down}...z[_three]e; fi  % tail
 penlabels([_zero],[_one],[_two],[_three],[_four]);
penlabels(0,a,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter ch_ldot - che lower dot";
cyrchar(ch_ldot,9.5u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0);
% ital_ch;
pickup fine.nib; interim superness:=more_super;
x2=x3; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
% left shstem;
if monospace:
 pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h; pos2(stem,0);
 filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else:
 x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
bot y4r=.75bar_height; y5=.75h; y3=min(y2-eps,.7[y4,y5]); y6+.5stem=h;
filldraw stroke z2'e...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
%%%
% the accent
% lower_dot;
_center:=x8;
_one:=10; _two:=11;
%
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
define_whole_blacker_pixels(dot_diam);
pos[_one](dot_diam,0); pos[_two](dot_diam,90);
x[_one]=hround(_center); bot y[_two]l=vround(-d-1);
if 0-oo-top y[_two]r<slab: y[_one]r:=vround(max(y[_two]l+eps,-fine-slab)); fi
x[_one]=x[_two]; y[_one]=vround(.5[y[_two]l,y[_two]r]);
dot([_one],[_two]);  % dot
penlabels(0,1,2,3,4,5,6,7, 8,9); endchar;
%
% variants of Serbian g p and t (not a serious experiment)
%
lhchar "Italic Cyrillic letter Vg - looks as dotless i bar";
cyrchar(Vg,5u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:u#,u# else: 0,0 fi); pickup fine.nib;
full_h:=h; h:=x_height;
x0=0; x5=w; x2=.5w+.25u; x3=.5w-.25u;
hook_in(0,1,2)(skewed); hook_out(3,4,5)(skewed);  % hooks
filldraw stroke z2e--z3e;  % stem
% the accent
h:=full_h;
% low_bar(.5w,0, 6,7);
x_move:=.5w; y_move:=0; _one:=6; _two:=7;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5); endchar;

lhchar "Italic Cyrillic letter Vp - looks as Latin u bar";
cyrchar(Vp,10u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); full_h:=h; h:=x_height;
pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
% the accent
h:=full_h;
% low_bar(.5w,0, 10,11);
x_move:=.5w; y_move:=0; _one:=10; _two:=11;
pickup if serifs: crisp.nib else: fine.nib fi;
numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
top y[_one]r=top y[_two]r=lc_trema_height+o;
x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
filldraw stroke z[_one]e--z[_two]e;  % bar
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Italic Cyrillic letter Vt - looks as Russian sh bar";
cyrchar(Vt,15u#,lc_trema_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:-2u#,-1.5u# else: 0,0 fi); full_h:=h; h:=x_height;
pickup fine.nib; interim superness:=more_super;
numeric shaved_stem; shaved_stem=mfudged.stem; save stem; stem=shaved_stem;
x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
if monospace: pos0(vair,90); x0=good.x .5[0,x2]; top y0l=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z2e{u,x_height}...{left}z0e; % terminal
else: x0=0;  hook_in(0,1,2)(skewed); fi % opening hook
pos2'(stem,-180); z2'=z2; pos4(vair,-90); pos5(min(stem,hair),0); x4=.5[x3,x5]; x5=x7';
pos5'(min(stem,hair),0); pos6'(vair,-90); pos7'(stem,-180); x5'=x7; y5'=y5;
pos8'(stem,-180); x8'=x7'; y8'+.5stem=h;
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x6=x7; y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
x7'+.5stem=hround(.5w+.5stem);
if monospace: bot y7=0; pos7(stem,0); else: x9=w; hook_out(7,8,9); fi
x6'=.6[x5',x7']; bot y6'r=bot y4r; y7'=.35x_height;
filldraw stroke super_arc.e(7',6')...{up}z5'e; %}}; middle stroke
filldraw circ_stroke z8'e--z7'e; % middle stem
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
% the accent
h:=full_h;
if monospace:
% low_bar(.5w,0, 10,11);
 x_move:=.5w; y_move:=0; _one:=10; _two:=11;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_move+hround(2.5u if monospace:/expansion_factor fi+.5vair);
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 x[_two]=x_move-hround(2.5u if monospace:/expansion_factor fi+.5vair);
 filldraw stroke z[_one]e--z[_two]e;  % bar
else:
% low_wbar(x1,x7,0, 10,11);
 x_left:=x1; x_right:=x7; y_move:=oo; _one:=10; _two:=11;
 pickup if serifs: crisp.nib else: fine.nib fi;
 numeric macron_breadth; macron_breadth:=Vround .2[vair,stem];
 pos[_one](macron_breadth,90); pos[_two](macron_breadth,90);
 x[_one]=x_left; x[_two]=x_right;
 top y[_one]r=top y[_two]r=lc_trema_height+o;
 filldraw stroke z[_one]e--z[_two]e;  fi % bar
penlabels(1,2,3,4,5,6,7,8,9, 10,11); endchar;

endinput;
%end of file
