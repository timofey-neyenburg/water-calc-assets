%
% lgcvacl.mf
%
%% Cyrillic font container with T2 encoding beta-support
%
% This file is future part of lxfonts package
% Version 3.5 // Patchlevel=0
% (c) O.Lapko
%
% This package is freeware product under conditions similar to
% those of D. E. Knuth specified for the Computer Modern family of fonts.
% In particular, only the authors are entitled to modify this file
% (and all this package as well) and to save it under the same name.
%
% Content:
%
% Lowercase Cyrillic acuted vowels
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% List of letternames
%
% a_ac
% e_ac
% yo_ac
% ukr_e_ac
% i_ac
% lat_i_ac
% ukr_i_ac
% o_ac
% ery_ac
% hrdsn_ac
% erev_ac
% yu_ac
% ya_ac
% yat_ac
% izh_ac
% b_yus_ac
% l_yus_ac
% il_yus_ac
% ib_yus_ac
% i_e_ac
% uk_ac
% y_ac
% ae_ac
% schwa_ac
% otld_ac
% dje_ac
%

LHver_check(3,5); % like |version_check| in ec

lhchar "Lowercase Cyrillic letter a_ac - a acute";
cyrchar_twice(a_ac,9u#,x_height#,0); % eng
bh#:=min(bar_height#,1.14x_height#-bar_height#); define_pixels(bh);
italcorr 1/3[bh#,x_height#]*slant+.5stem#-serif_fit#-2u#;
adjust_fit(0,serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi);
% lower_a;
pickup fine.nib; top y3r=h+vround 1.5oo;
if serifs: pos1(flare,180); pos2(hair,180);
 pos3(vair,90);  lft x1r=hround max(u,2.1u-.5flare); x3=.5w-.5u;
 y1=min(bh+.5flare+2vair+2,.9[bh,h]-.5flare);
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=.5w-.2u; top y1r=vround .82[bh,top y3r];
 filldraw stroke term.e(3,1,left,.9,4); fi  % terminal
pos4(stem,0); rt x4r=hround(w-2.5u+.5stem); y4=1/3[bh,h];
pos5(stem,0); x5=x4; y5=max(.55bh,2vair);
filldraw stroke super_arc.e(3,4)&z4e..z5e;  % arc and stem
pos6(.3[thin_join,vair],90); x6=x4; bot y6=bh;
pos7(hround(curve-2stem_corr),180);
lft x7r=hround max(.5u,1.5u-.5curve); y7=1/3[top y8l,top y6r];
pos8(vair,270); x8l=.5w-.75u; bot y8r=-oo;
pos9(thin_join,360); z9l=z5l; (x,y8r)=whatever[z8l,z9l]; x8r:=max(x,x8-u);
{{interim superness:=more_super;
 filldraw stroke z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
if serifs: numeric shaved_stem; shaved_stem=hround(stem-3stem_corr);
 if hair#+.5stem#>1.5u#: pickup tiny.nib;
  pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=.2[.5tiny,bh];
  pos11(shaved_stem,0); rt x11r=hround(w-.25u); bot y11=0;
  pos12(shaved_stem,0); x11=x12; top y12=slab+eps;
  filldraw z5'l---z10l...z11l{right}--z11r
   --z12r{left}...z10r+.75(z12-z11)---z5'r--cycle;  % foot
 else: pickup crisp.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=1/3bh;
  pos11(.2[vair,stem],90); x11r=.5[x10r,x12r]; bot y11l=-vround .5oo;
  pos12(hair,180); rt x12l=hround(w-.1u); y12=max(y10,y11+vair);
  pos13(hair,180); x13=x12; top y13=max(vround .6bh,top y12);
  (x',y11l)=whatever[z11r,z12r]; x11l:=max(x',x10);
  filldraw stroke z5'e---z10e...z11e{right}...z12e---z13e; fi  % hook
else: numeric shaved_stem; shaved_stem=hround(stem-stem_corr);
 pickup tiny.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
 pos10(shaved_stem,0); x10=x5'; bot y10=0;
 filldraw stroke z5'e--z10e; fi  % base of stem
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

lhchar "Lowercase Cyrillic letter e_ac - e acute";
cyrchar_twice(e_ac,7.25u#+max(.75u#,.5curve#),x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
% lower_e;
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,0);
pos2(vair,90); pos3(left_curve,180);
y1=good.y bar_height; top y2r=h+vround 1.5oo; y0l=bot y1;
rt x1r=hround min(w-.5u,w-u+.5right_curve);
lft x3r=hround max(.5u,1.25u-.5left_curve); x2=.5w+.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo; x4=x2+.25u;
if serifs:
 pos4(vair',270); pos5(hair,360);
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else:
 pos4(vair,270);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,270); z4=z4';
 pos5(.5[vair,flare],275); rt x5r=hround(w-.6u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,right,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5); endchar;

lhchar "Lowercase Cyrillic letter yo_ac - yo acute";
cyrchar_twice(yo_ac,7.25u#+max(.75u#,.5curve#),
        x_height#,0); % eng
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
% lower_e;
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,0);
pos2(vair,90); pos3(left_curve,180);
y1=good.y bar_height; top y2r=h+vround 1.5oo; y0l=bot y1;
rt x1r=hround min(w-.5u,w-u+.5right_curve);
lft x3r=hround max(.5u,1.25u-.5left_curve); x2=.5w+.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo; x4=x2+.25u;
if serifs:
 pos4(vair',270); pos5(hair,360);
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else:
 pos4(vair,270);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,270); z4=z4';
 pos5(.5[vair,flare],275); rt x5r=hround(w-.6u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,right,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
%%%%%
% the accents
lowercase_umlaut(0,0, 6,7,8,9);
hpos_corr=-u; put_accent(cyrcaph); repeat_once; % cyrcaph for umlaut
penlabels(0,1,2,3,4,5, 6,7,8,9); endchar;

lhchar "Lowercase Cyrillic letter ukr_e_ac - Ukrainian e acute";
cyrchar_twice(ukr_e_ac,8u#,x_height#,0); % eng
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace:.5u#,.5u# else:-.1u#,0 fi); full_h:=h; h:=x_height;
% lower_ukre;
set_bar_axis;
pickup fine.nib; pos2(vair',90); pos3(curve,180); pos4(vair',270);
x2=x4=(.5(w+u)); top y2r=vround(h+1.5oo); bot y4r=-oo;
lft x3r=(hround max(.6u,1.35u-.5curve)); y3=.5h;
if serifs:
 pos1(hair,0); pos5(hair,360);
 y1=min(y2.bh+.75flare-.25vair+eps,.9[bar_height,h]-.75flare);
 rt x1r=rt x5r=hround(w-.5u);
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
 filldraw stroke z1e{x2-x1,5(y2-y1)}...pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
 pos6(.3[fine.breadth,hair],0); x6r=x1r; top y6=h+oo;
 x1r-x1'=1.5curve; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,5(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else:
 pos1(4/7[vair',flare],80); pos5(.6[vair',flare],275);
 rt x1r=hround(w-.6u);
 top y1r=vround .82[bar_height,top y2r];
 filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
 rt x5r=hround(w-.5u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
 if angle direction 1 of p.e>75:
  p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
%%%
% lukre_bar;
if (serifs=true) and (bar#<=.5curve#):
   x7r=x3l; x8r=min(lft x1l-.5hair,0.85[x3l,x1l]);
   y7l=y8l=y1.bh; y7r=y8r=y2.bh; x7l=x7r; x8l=x8r;
   numeric bar[];
   bar2=7/8[bar,fudged.stem-2stem_corr];
   penpos9(bar2,45); z9=.575[z7l,z8l];
   fill circ_stroke z8e{-2,-1}..{-2,1}z9e..{-2,-1}z7e; % curve /\/
else:
   bar_stroke(x3l,min(lft x1l-.5hair,0.85[x3l,x1l]));  % bar --
fi
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,1',2,3,4,5,6,7,8,9); endchar;

lhchar "Lowercase Cyrillic letter i_ac - i acute";
cyrchar_twice(i_ac,10u# if not serifs:+.5width_adj# fi, % n_width
 x_height#,0);
italcorr x_height#*slant-serif_fit#+jut#-2u#+.5stem#; %d->x_height+jut
adjust_fit(serif_fit#,serif_fit#);
begingroup
% lower_i;
pickup tiny.nib; pos1(fudged.stem,0); pos2(fudged.stem,0);
pos3(fudged.stem,0); pos4(fudged.stem,0);
lft x1l=lft x2l=hround(2.5u-.5fudged.stem-eps); x3=x4=w-x1;
top y1=top y3=h; bot y2=bot y4=0;
filldraw stroke z1e--z2e; % left stem
filldraw stroke z3e--z4e; % right stem
numeric stem[],alpha[];
if hefty: numeric upper_notch,lower_notch;
 stem1=fudged.hair-2stem_corr;
 upper_notch=y6-cap_notch_cut-eps; lower_notch=y5+cap_notch_cut+eps;
 if monospace:
  x5r =rt x1r; x6l =lft x3l; y5=vround(vair'+eps); y6=vround(h-vair-eps);
  alpha1=diag_ratio(1,stem1,y5-y6,x5r-x6l);
  penpos5(min(2fudged.stem,alpha1*stem1),0);
  penpos6(min(2fudged.stem,alpha1*stem1),0);
  x5'=lft x1l; z5'=whatever[z5l,z6l]; x6'=rt x4r; z6'=whatever[z5r,z6r];
  x5l:=x5'; x6r:=x6'; y5l:=y5'; y6r:=y6';
 else:
  y5l=0; y6r=h; x5=x1r; x6=x3l; penpos5(stem1,0); penpos6(stem1,0);
 fi
 x2'=rt x1r; z2'=whatever[z5l,z6l]; x3'=lft x4l; z3'=whatever[z5r,z6r];
 fill z5l..
  if y2'>lower_notch: {right}(x2'+1,lower_notch){up}... fi
  {z6-z5}diag_in(5l,6l,1,6r)--z6r..
  if y3'<upper_notch: {left}(x3'-1,upper_notch){down}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonal
else:
 x5=hround (rt x1r-.5)-eps; x6=hround (lft x3l+.5)+eps;
 y5l=vround(y2+bracket+eps); y6r=vround(y3-bracket-eps);
 stem1=max(tiny.breadth,fudged.hair-2stem_corr);
 alpha1=diag_ratio(1,stem1,x6-x5,top y6r-bot y5l);
 penpos5(alpha1*stem1,90); penpos6(alpha1*stem1,90);
 fill stroke z5e--z6e; fi % diagonal
%%%
if serifs:
 numeric inner_jut;
 if rt x1r+jut+.5u+1<=lft x3l-jut: inner_jut=jut;
  else: rt x1r+inner_jut+.5u+1=lft x3l-inner_jut; fi
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(1,2,a,1/3,jut,b,1/3,inner_jut);	% upper left serif
 dish_serif(3,4,e,1/3,inner_jut,f,1/3,jut);	% upper right serif
 slab:=vair; % lower.slab
 dish_serif(2,1,c,1/3,jut,d,1/3,inner_jut);	% lower left serif
 dish_serif(4,3,g,1/3,inner_jut,h,1/3,jut); fi	% lower right serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6); endchar;

lhchar "Lowercase Cyrillic letter lat_i_ac - Ukrainian i acute";
%numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
cyrchar_twice(lat_i_ac,5u#,x_height#,0);
%define_whole_blacker_pixels(dot_diam);
italcorr h#*slant-serif_fit# + .5idot_diam# -2u#;
adjust_fit(serif_fit#+stem_shift# if monospace:+.25u#fi,serif_fit#-stem_shift#);
begingroup
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
pos3(idot_diam,0); pos4(idot_diam,90);
if serifs: x3r=max(x1r,x1+.5(idot_diam-tiny)-.2jut) else: x3=x1-.5 fi;
top y4r=h+1;
if bot y4l-top y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; % dot(3,4);  % dot
if serifs: sloped_serif.l(1,2,a,1/3,.95jut,serif_drop);  % upper serif
 save slab; slab:=vair; % lower.slab
 dish_serif(2,1,b,1/3,jut,c,1/3,.9jut); fi  % lower serif
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4); endchar;

lhchar "Lowercase Cyrillic letter ukr_i_ac - yi acute";
cyrchar_twice(ukr_i_ac,5u#,x_height#,0);
italcorr h#*slant-serif_fit# + .5idot_diam# -2u#;%!!!from ec
adjust_fit(serif_fit#+stem_shift# if monospace:+.25u#fi,serif_fit#-stem_shift#);
begingroup
if odd(w-stem'): change_width; fi
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
lft x1l=hround(.5w-.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs: sloped_serif.l(1,2,a,1/3,.95jut,serif_drop);  % upper serif
 save slab; slab:=vair; % lower.slab
 dish_serif(2,1,b,1/3,jut,c,1/3,.9jut); fi  % lower serif
endgroup;
% the accents
lowercase_umlaut(0,0, 3,4,5,6);
hpos_corr=-u; put_accent(cyrcaph); repeat_once; % cyrcaph for umlaut
penlabels(0,1,2, 3,4,5,6); endchar;

lhchar "Lowercase Cyrillic letter o_ac - o acute";
cyrchar_twice(o_ac,9u#,x_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
% lower_o;
penpos1(vair,90); penpos3(vair',-90); penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=h+vround 1.5oo; y3r=-oo;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4); endchar;

lhchar "Lowercase Cyrillic letter u_ac - u acute";
cyrchar_twice(u_ac,if serifs:9.5u# else:9u# fi,x_height#,desc_depth#); % eng
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
begingroup
% lower_u;
numeric left_stem,right_stem,bot_stem,bot_vair,outer_jut;
left_stem=fudged.stem-stem_corr; right_stem=fudged.hair if hefty:-2stem_corr fi;
bot_stem=fudged.hair if hefty:-8stem_corr fi;
bot_vair=Vround(if serifs: vair else:.5[vair,bot_stem] fi); outer_jut=.75jut;
x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4r=h; y2=y3=0; x2l=x3l;
numeric alpha,alpha[]; x9=3u; y9=bot_vair-d-oo;
alpha1=diag_ratio(2,bot_stem,y1-y3,x4r-x1l-apex_corr);
alpha2=diag_ratio(1,bot_stem,y1-y9,x4r-x9);
if alpha1<alpha2:
 x2l-x1l=x4r-x3r+apex_corr; alpha=alpha1;
else:
 alpha=alpha2; z3l=whatever[z9,z4r-(alpha*bot_stem,0)]; fi
penpos3(alpha*bot_stem,0); penpos4(alpha*right_stem,0);
alpha3=(y1++(x2l-x1l))/y1;
penpos1(alpha3*left_stem,0); penpos2(alpha3*left_stem,0);
z0=whatever[z1r,z2r]=z4l+whatever*(z3r-z4r);
if y0>notch_cut:
 y0:=notch_cut;
 fill z0+.5right{up}...{z4r-z3r}diag_end(0,4l,1,1,4r,3r)
  --z3r--z2l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{down}z0+.5left--cycle; % left and right diagonals
else:
 fill z0--diag_end(0,4l,1,1,4r,3r)--z3r--z2l
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(alpha*bot_stem,0); z5r=whatever[z3r,z4r]; y5-.5vair=-.5d;
if serifs:
 numeric light_bulb; light_bulb=hround 7/8[hair,flare]; clearpen;
 penpos6(vair,-90); penpos7(hair,-180); penpos8(light_bulb,-180);
 x6=2u; y6r=-d-oo; y8-.5light_bulb=-.85d; x8r=hround .35u;
 fill stroke z3e---z5e...{left}z6e; bulb(6,7,8);  % arc and bulb
 numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);      % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark); % upper right serif
else:
 penpos6(bot_vair,-90); x6=2.5u; y6r=-d-oo;
 fill stroke z3e---z5e...{left}z6e;  % arc
 pickup fine.nib; pos6'(bot_vair,-90); z6'=z6;
 pos7(2/3[bot_vair,flare],-85);
 lft x7l=hround u; bot y7r=vround-.96d-oo; y7l:=good.y y7l;
 filldraw stroke term.e(6',7,left,1,4); fi % arc and terminal
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;


lhchar "Lowercase Cyrillic letter ery_ac - ery acute";
cyrchar_twice(ery_ac,9u#,x_height#,0); % v_witdh
italcorr .75x_height#*slant-.5u#;
adjust_fit(if monospace: 0,0 else: serif_fit#,serif_fit# fi %wlett_adj
 +4u#);
begingroup
% lower_ery;
pickup tiny.nib; pos1(fudged.stem,0); pos2(fudged.stem,0);
lft x1l=lft x2l=hround (2.5u-.5fudged.stem-eps); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs:
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(1,2,a,1/3,jut,b,1/3,jut);		% upper serif
 slab:=vair; % lower.slab
 nodish_serif(2,1,c,1/3,jut,d,1/3,.5jut); fi	% lower serif
% lsftsn_bowl(2);
_zero:=2;
_one:=3; _two:=4; _three:=5; _four:=6; _five:=7;
%
set_bar_axis;
penpos[_three](curve if hefty:-3stem_corr fi,0);
penpos[_four](vair',-90); penpos[_five](vair',-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_two]l,y[_four]l];
y[_one]l=y[_two]l=y1.bh; y[_one]r=y[_two]r=y2.bh;
x[_two]l=x[_two]r=x[_four]=w-3.75u; %.5v_width+.75u;
x[_one]l=x[_one]r=x[_zero]; x[_three]r=hround(w-.6u);
x[_two]l:=x[_four]l:=x[_two]r-.25curve;
fill stroke
 {{if not serifs: interim superness:=more_super; fi
  z[_five]e..super_arc.e([_four],[_three])}} &
 super_arc.e([_three],[_two])..z[_one]e;  % lobe
%%%
% I
w:=w+4u; rt x8r=rt x9r=w-lft x1l; top y8=h; bot y9=0;
pos8(fudged.stem,0); pos9(fudged.stem,0);
filldraw stroke z8e--z9e; % stem
if serifs: save slab; slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(8,9,e,1/3,jut,f,1/3,jut);	  % upper serif
 slab:=vair; % lower.slab
 dish_serif(9,8,g,1/3,jut,h,1/3,jut); fi  % lower serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

lhchar "Lowercase Cyrillic letter hrdsn_ac - hard sign acute";
cyrchar_twice(hrdsn_ac,9u#,x_height#,0); % v_witdh
italcorr .75x_height#*slant-.5u#;
adjust_fit(2u#-.5width_adj#,if monospace:.5u# else:0 fi);
begingroup
% lower_hrdsn;
pickup tiny.nib; pos1(fudged.stem,0); pos2(fudged.stem,0);
lft x1l=lft x2l=hround (2.5u-.5fudged.stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
% lower left beak
pickup crisp.nib; top y3r=h; x3=x1; pos3(vair,90);
pos4(hair,180); lft x4r=l+letter_fit+hround.45u; y4=good.y(y3l-beak/1.4)-eps;
arm(3,4,q,beak_darkness,-.4beak_jut);
if serifs:
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 nodish_serif(1,2,a,1/3,.5jut,b,1/3,.5jut);	% upper serif
 slab:=vair; % lower.slab
 nodish_serif(2,1,c,1/3,jut,d,1/3,.5jut); fi	% lower serif
% lsftsn_bowl(2);
_zero:=2;
_one:=5; _two:=6; _three:=7; _four:=8; _five:=9;
%
set_bar_axis;
penpos[_three](curve if hefty:-3stem_corr fi,0);
penpos[_four](vair',-90); penpos[_five](vair',-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_two]l,y[_four]l];
y[_one]l=y[_two]l=y1.bh; y[_one]r=y[_two]r=y2.bh;
x[_two]l=x[_two]r=x[_four]=w-3.75u; %.5v_width+.75u;
x[_one]l=x[_one]r=x[_zero]; x[_three]r=hround(w-.6u);
x[_two]l:=x[_four]l:=x[_two]r-.25curve;
fill stroke
 {{if not serifs: interim superness:=more_super; fi
  z[_five]e..super_arc.e([_four],[_three])}} &
 super_arc.e([_three],[_two])..z[_one]e;  % lobe
%%%
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

lhchar "Lowercase Cyrillic letter erev_ac - reverse e acute";
cyrchar_twice(erev_ac,8u#,x_height#,0); % eng
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
% lower_erev;
set_bar_axis;
pickup fine.nib; pos2(vair',90); pos3(curve,0); pos4(vair',-90);
x2=x4=w-.5(w+u); top y2r=vround(h+1.5oo); bot y4r=-oo;
rt x3r=w-hround max(.6u,1.35u-.5curve); y3=.5h; % eng
if serifs:
 pos1(hair,180); pos5(hair,-180);
 y1=min(y2.bh+.75flare-.25vair+eps,.9[bar_height,h]-.75flare);
 lft x1r=lft x5r=hround.5u;
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=max(x,x4l-.5u);
 filldraw stroke z1e{x2-x1,5(y2-y1)}...pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
 pos6(.3[fine.breadth,hair],180); x6r=x1r; top y6=h+oo;
 x1'-x1r=1.5curve; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,5(y2-y1)}..z2{right};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else:
 pos1(4/7[vair',flare],100); pos5(.6[vair',flare],-95);
 lft x1r=good.x .6u; top y1r=vround .82[bar_height,top y2r];
 filldraw stroke term.e(2,1,left,.8,4);  % upper terminal
 lft x5r=good.x .5u;
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{left}..tension .9 and 1..z5e;
  if angle direction 1 of p.e<105:
   p.e:=z4e{left}..tension atleast.9 and 1..{dir 105}z5e; fi
 endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
%%%
% lerev_bar;
if (serifs=true) and (bar#<=.5curve#):
   x7r=max(rt x1l+.5hair,0.85[x3l,x1l]); x8r=x3l;
   y7l=y8l=y1.bh; y7r=y8r=y2.bh; x7l=x7r; x8l=x8r;
   numeric bar[];
   bar2=7/8[bar,fudged.stem-2stem_corr];
   penpos9(bar2,45); z9=.475[z7l,z8l];
   fill circ_stroke z7e{2,1}..{2,-1}z9e..{2,1}z8e; % curve /\/
else:
   bar_stroke(max(rt x1l+.5hair,0.85[x3l,x1l]),x3l); % bar --
fi
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,1',2,3,4,5,6,7,8); endchar;

lhchar "Lowercase Cyrillic letter yu_ac - yu acute";
cyrchar_twice(yu_ac,9u#,x_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: 4u# else: 4.5u# fi +serif_fit#, %li_part_adj
 if monospace: -.45u# else: 0 fi);
begingroup
% lower_yu;
penpos1(vair,90); penpos3(vair',-90); penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=h+vround 1.5oo; y3r=-oo;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
save slab; % lower.slab
% barh_i_part(2);
_zero:=2;
_one:=5; _two:=6;
%
pickup tiny.nib;
pos[_one](stem,0); pos[_two](stem,0);
lft x[_one]l=lft x[_two]l=
    l+letter_fit+if not monospace: serif_fit+ fi hround (2.5u-.5stem-eps);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
bar_stroke(x[_one],x[_zero]); % bar
if serifs:
   slab:=Vround(vair-vair_corr); % lower.slab
   dish_serif([_one],[_two],s,1/3,jut,t,1/3,jut); % upper serif
   slab:=vair; % lower.slab
   dish_serif([_two],[_one],u,1/3,jut,v,1/3,jut); % lower serif
fi
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8); endchar;

iff not concrete:%
iff not specific:%
lhchar "Lowercase Cyrillic letter ya_ac - ya acute (`roman')";
cyrchar_twice(ya_ac,if serifs: 9.75u# else: 10u# fi-.5width_adj#,x_height#,0); %ya (~n_width:10u)
italcorr x_height#*slant-serif_fit#+jut#-2u#+.5stem#; %d->x_height+jut
adjust_fit(if serifs:serif_fit# else:0 fi,serif_fit#);
begingroup
% lowerr_ya;
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
rt x1r=rt x2r=hround(w-2.5u+.5stem'); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
penpos3(vair,90); penpos4(vair,90); penpos5(curve if hefty:-3stem_corr fi,180);
y6r=y7r=hround (if serifs:.52h else:.5h fi-.6[thin_join,vair]/2);
y6l=y7l=y6r+.6[thin_join,vair]; y6=y7=.5[y6l,y6r];%
z3r=top z1; y4=y3; y5=.52[y6r,y4r]+eps;
x7l=x7r=x2; x4+.25u=x6l=x6r; x4=.5w if serifs:+.25u else:-.25u fi;
x5r=hround min(1.25u,2u-.5curve)+eps;
x4l:=x4+.125curve; x6l:=x6l+.125curve;
fill stroke
{{interim superness:=more_super; z7e..pulled_arc.e(6,5) &
   pulled_arc.e(5,4)..z3e}};  % lobe
numeric stem[],alpha,right_jut;
right_jut=if serifs:.6jut else:.4tiny fi;
stem1=max(tiny.breadth,hround(fudged.stem-3stem_corr));
lft x9l=hround(l+letter_fit+.3u+right_jut)-eps; bot y9=0;
alpha=diag_ratio(1,.5(stem1-tiny),y1-y9,x9l-x1);
penpos9(alpha*(stem1-tiny),0);
x8=x6l-if serifs:.75u else:.25u fi; y8=y6;
forsuffixes $=l,r: y9'$=0; endfor
z9'r=z9r+penoffset z1-z9 of currentpen+whatever*(z1-z9);
z9'l=z9l+penoffset z9-z1 of currentpen+whatever*(z1-z9);
z8=z9'+whatever*(z1-z9);
penpos8(if serifs: x9'r-x9'l else: stem fi-2stem_corr,0);
fill z8l--diag_end(8l,9'l,1,.5,9'r,8r)--z8r--cycle;  % diagonal
if serifs:
 numeric inner_jut;
 if lft x2l-jut-.5u+1>=rt x9r+jut: inner_jut=jut;
  else: lft x2l-jut-.5u+1=rt x9r+inner_jut; fi
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 nodish_serif(1,2,a,1/3,.5jut,b,1/3,jut); % upper serif
 slab:=vair; % lower.slab
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower serif
 dish_serif(9,8,g,1/2,right_jut,h,1/3,inner_jut)(dark); fi % lower diagonal serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Lowercase Cyrillic letter yat_ac - yat acute";
cyrchar_twice(yat_ac,9u#,asc_height#,0);
italcorr .5x_height#*slant+min(.5curve#-u#,-.25u#);
adjust_fit(if serifs: serif_fit# else: 0 fi,if monospace:.5u# else:0 fi);
begingroup
% lower_yat;
pickup tiny.nib; pos1(fudged.stem,0); pos2(fudged.stem,0);
lft x1l=lft x2l=hround (2.5u-.5fudged.stem);
top y1=x_height+ if serifs: 1.5u else: 2u fi; bot y2=0;
filldraw stroke z1e--z2e; % body of stem
if serifs: save slab; slab:=vair; % lower.slab
 nodish_serif(2,1,a,1/3,jut,b,1/3,.5jut) ; fi % lower serif
% lsftsn_bowl(2);
_zero:=2;
_one:=3; _two:=4; _three:=5; _four:=6; _five:=7;
%
set_bar_axis;
penpos[_three](curve if hefty:-3stem_corr fi,0);
penpos[_four](vair',-90); penpos[_five](vair',-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_two]l,y[_four]l];
y[_one]l=y[_two]l=y1.bh; y[_one]r=y[_two]r=y2.bh;
x[_two]l=x[_two]r=x[_four]=w-3.75u; %.5v_width+.75u;
x[_one]l=x[_one]r=x[_zero]; x[_three]r=hround(w-.6u);
x[_two]l:=x[_four]l:=x[_two]r-.25curve;
fill stroke
 {{if not serifs: interim superness:=more_super; fi
  z[_five]e..super_arc.e([_four],[_three])}} &
 super_arc.e([_three],[_two])..z[_one]e;  % lobe
%%%
y10=y1+2jut;
if hefty: filldraw z1l--(x1l,y10)--(x1r,y10)--z1r--cycle; % stem
else: x10=x1l-jut; x11=x1r+jut; y10=y11;
 filldraw z1l--z10--z1r--cycle; filldraw z1l--z11--z1r--cycle; fi % swallow
% hcross
pickup crisp.nib; pos8(bar,90); pos9(bar,90);
lft x8=hround1/3u if serifs:-serif_fit fi; x9=x1r+2.5u;
top y8r=top y9r=vround x_height+eps;
filldraw stroke z8e--z9e; % cross bar
%%%%%
endgroup;
% the accent
put_accent(cyrcaph); repeat_once; % cyrcaph for asc_height
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

lhchar "Lowercase Cyrillic letter izh_ac - izhitsa acute";
cyrchar_twice(izh_ac,if serifs: 9.5u# else:9u# fi,x_height#,0);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+.5u#fi,
 if serifs: max(.75u#,.25u#+.5flare#) else:max(0,.5stem#-.5u#) fi); % lizh_adj
begingroup
full_h:=h; h:=x_height;
% lower_izh;
numeric left_stem,right_stem,outer_jut,alpha,bulb_diam;
left_stem=stem-stem_corr; bulb_diam=7/8[hair,flare];
right_stem=min(hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75jut; x1l=l+letter_fit+outer_jut+.25u; x4r=w-x1l; y1=y4=h;
x2-x1=x4-x3; x2l+apex_corr=x3l; y2=y3=-apex_o;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
penpos4'(alpha*right_stem,0); penpos5(vair,90);
x5=if serifs: .5[x4',x6r] else:x6r fi; y5r=h+oo;
x6r=hround(r-.35u);
y6=max(vround(y5r-.25tiny-.5flare),
  min(bar_height+.5flare+2vair'+2,.9[bar_height,h]-.5flare)); %zh bulb h
z4'=whatever[z3,z4]; y4'=min(y6-.5hair,8/5bar_height)-eps;
if serifs: penpos6(hair,0); penpos7(flare,0);
 cyrbulb(5,6,7); fi % bulb
if y0>cap_notch_cut: y0:=cap_notch_cut;
 fill z0+.5right{up}...{z4-z3}z4'l...{right}
      if serifs: z5r--z5l
      else:
         (.5[x4,x4r],y5r)--(fine.lft x5r,y5r)..(x5r,fine.bot y5r)--
         (x5l,fine.top y5l)..(fine.lft x5l,y5l) fi
  {left}...z4'r{z3-z4}
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--z4'l{z4-z3}...{right}z5r--z5l{left}...{z3-z4}z4'r
  --diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2);
 if rt x1'r+jut+.5u+1<=lft x4l-jut+.5tiny: inner_jut=jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4l-inner_jut+.5tiny; fi
 save slab; slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut); fi % upper left serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7); endchar;

iff not specific:%
lhchar "Lowercase Cyrillic letter b_yus_ac - big yus acute (`roman')";
cyrchar_twice(b_yus_ac,15u#,x_height#,0);
adjust_fit(serif_fit#,serif_fit#);
begingroup
if odd(fudged.stem-w): change_width; fi % symmetric & all three equal
% lowerr_yus;
% ltop_yus;
set_bar_axis; numeric stem[];
stem1=fudged.stem-3stem_corr; stem2=min(stem1,fudged.hair);
penpos1(stem1,0); penpos2(stem2,0); penpos3(stem1,0); penpos4(stem2,0);
x1l=w-x2r=hround(.2w-.5tiny-.5)-eps; y1=y2=h; y3=y4=3/5[y1.bh,y2.bh];
x3l=hround(.5w-.5stem1); x3r=x4r;
z0=whatever[z4l,z2l]=whatever[z1r,z3r];
if hefty: y0:=y2.bh;
 fill z3l--diag_end(3l,1l,1,1,1r,3r){z3r-z1r}
  ...{down}(x0-.5,y0)..(x0+.5,y0){up}...
  {z2l-z4l}diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; % diagonals
else: fill z3l--diag_end(3l,1l,1,1,1r,3r)--z0--
  diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; fi % diagonals
penpos5(stem1,0); x5=x3; y5=0;
fill z3l--diag_end(3l,5l,1,1,5r,3r)--z3r--cycle; %middle stem
z6=whatever[z4,z2]; z7=whatever[z1,z3]; y6=y7=good.y(y1-vair);
fill z1--z7--z6--z2--cycle; % upper bar
% lyus_rbowl;
 numeric stem[],alpha[],right_jut;
 right_jut=if serifs:.6jut else:.4tiny fi;
 y10l=y1.bh; y10r=y2.bh; x10l=x10r=x3; y9=y12=min(bot y10l-.5,.85y10l);
 rt x8r=w-lft x11l=hround(r-letter_fit-.3u-right_jut); bot y8=bot y11=y5;
 stem2=max(tiny.breadth,fudged.stem-3stem_corr);
 alpha2=diag_ratio(1,.5(stem2-tiny),h-y8,x8r-x3);
 penpos8(alpha2*(stem2-tiny),0); penpos11(alpha2*(stem2-tiny),0);
 forsuffixes $=l,r:  y8'$=0; y11'$=0; endfor
 z8'r=z8r+penoffset (x3,h)-z8 of currentpen+whatever*((x3,h)-z8);
 z8'l=z8l+penoffset z8-(x3,h) of currentpen+whatever*((x3,h)-z8);
 z11'r=z11r+penoffset (x3,h)-z11 of currentpen+whatever*((x3,h)-z11);
 z11'l=z11l+penoffset z11-(x3,h) of currentpen+whatever*((x3,h)-z11);
 z8'=.5[z8'l,z8'r];  z11'=.5[z11'l,z11'r];
 z9=z8'+whatever*((x3,h)-z8); z12=z11'+whatever*((x3,h)-z11);
 penpos9(stem-2stem_corr,0); penpos12(stem-2stem_corr,0);
 fill z10l{right}...{z8'l-z9l}z9l--diag_end(9l,8'l,.5,1,8'r,9r)
  --z9r{z9r-z8'r}...{left}z10r--cycle; % right diagonal
 fill z10r{left}...{z11'l-z12l}z12l--diag_end(12l,11'l,1,.5,11'r,12r)
  --z12r{z12r-z11'r}...{right}z10l--cycle; % left diagonal
%%
if serifs: numeric inner_jut; prime_points_inside(5,3);
 if rt x5r+jut+.5u+1<=lft x8l-jut: inner_jut=jut;
  else: rt x5r+jut+.5u+1=lft x8l-inner_jut; fi
 save slab; slab:=vair; % lower.slab
 dish_serif(11,12,a,1/3,right_jut,b,1/2,inner_jut)(dark); % lower diagonal serif
 dish_serif(8,9,e,1/2,inner_jut,f,1/3,right_jut)(dark);   % lower diagonal serif
 dish_serif(5',3,c,1/3,.75jut,d,1/3,.75jut); fi           % lower middle serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

lhchar "Lowercase Cyrillic letter l_yus_ac - little yus acute";
cyrchar_twice(l_yus_ac,10u#+2stem#,x_height#,0);
adjust_fit(serif_fit#,serif_fit#);
begingroup
% lower_amod;
numeric left_stem,right_stem,outer_jut,alpha;
right_stem=fudged.stem-stem_corr;
left_stem=min(fudged.hair if hefty:-2stem_corr fi,right_stem);
outer_jut=.8jut; pickup tiny.nib; y1=y4=0;
x1l=w-x4r=l+letter_fit+outer_jut+.25u+eps;
%
x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+.5[apex_oo,apex_o];%+apex_o+apex_oo
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-notch_cut: y0:=h-notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1,z2];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
if hefty: y5r else: y5 fi =.5y0;
y5r-y5l=y6r-y6l=.6[thin_join,vair];
penstroke z5e--z6e; % bar line
pickup tiny.nib; x7=.5[x5,x6]; top y7=y5; bot y8=0;
pos7(max(tiny.breadth+eps,.7[thin_join,stem]),0);
pos8(max(tiny.breadth+eps,.7[thin_join,stem]),0);
x7=x8; filldraw stroke z8e--z7e; % middle stem
if serifs: prime_points_inside(8,7); numeric inner_jut;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 save slab; slab:=vair; % lower.slab
 dish_serif(1',2,a,1/2,outer_jut,b,.6,.6inner_jut)(dark); % lower left diagonal serif
 dish_serif(4',3,c,1/2,.6inner_jut,d,1/3,outer_jut);      % lower right diagonal serif
 dish_serif(8,7,e,1/3,.6inner_jut,f,1/3,.6inner_jut); fi  % lower middle stem serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8); endchar;

lhchar "Lowercase Cyrillic letter il_yus_ac - iot little yus acute";
cyrchar_twice(il_yus_ac,10u#+2stem#,x_height#,0);
adjust_fit(if monospace:4u# else:4.75u# fi+serif_fit#,serif_fit#);
begingroup
% lower_amod;
numeric left_stem,right_stem,outer_jut,alpha;
right_stem=fudged.stem-stem_corr;
left_stem=min(fudged.hair if hefty:-2stem_corr fi,right_stem);
outer_jut=.8jut; pickup tiny.nib; y1=y4=0; x1l=w-x4r=outer_jut+.25u+eps;
%
x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+.5[apex_oo,apex_o];%+apex_o+apex_oo
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-notch_cut: y0:=h-notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1,z2];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
if hefty: y5r else: y5 fi =.5y0;
y5r-y5l=y6r-y6l=.6[thin_join,vair];
penstroke z5e--z6e; % bar line
pickup tiny.nib; x7=.5[x5,x6]; top y7=y5; bot y8=0;
pos7(max(tiny.breadth+eps,.7[thin_join,stem]),0);
pos8(max(tiny.breadth+eps,.7[thin_join,stem]),0);
x7=x8; filldraw stroke z8e--z7e; % middle stem
if serifs: prime_points_inside(8,7); numeric inner_jut;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 save slab; slab:=vair; % lower.slab
 dish_serif(1',2,a,1/2,outer_jut,b,.6,.6inner_jut)(dark); % lower left diagonal serif
 dish_serif(4',3,c,1/2,.6inner_jut,d,1/3,outer_jut);      % lower right diagonal serif
 dish_serif(8,7,e,1/3,.6inner_jut,f,1/3,.6inner_jut); fi  % lower middle stem serif
%%%%%
x9=x5; y9=.5[y5l,y5r];
% i_part(9);
_zero:=9;
_one:=10; _two:=11; _three:=12; _four:=13;
%
pickup tiny.nib; pos[_one](stem,0); pos[_two](stem,0);
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if not monospace: serif_fit+ fi hround(2.5u-.5stem-eps);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
if serifs: slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif([_one],[_two],s,1/3,jut,t,1/3,jut); % upper stem serif
 slab:=vair; % lower.slab
 dish_serif([_two],[_one],u,1/3,jut,v,1/3,jut); fi % lower stem serif
x[_three]=x[_zero]; x[_four]=x[_one];
y[_three]=y[_four]=y[_zero]; pickup fine.nib;
penpos[_three](.6[thin_join,vair],90);
penpos[_four](.6[thin_join,vair],90); %fi
fill stroke z[_three]e--z[_four]e; % bar
%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

iff not specific:%
lhchar "Lowercase Cyrillic letter ib_yus_ac - iot big yus acute (`roman')";
cyrchar_twice(ib_yus_ac,15u#,x_height#,0);
adjust_fit(.75u#+
        if monospace: 4u# else: 4.5u# fi +serif_fit#, %li_part_adj+.75u#
 serif_fit#);
begingroup
if odd(fudged.stem-w): change_width; fi % symmetric & all three equal
% lowerr_yus;
% ltop_yus;
set_bar_axis; numeric stem[];
stem1=fudged.stem-3stem_corr; stem2=min(stem1,fudged.hair);
penpos1(stem1,0); penpos2(stem2,0); penpos3(stem1,0); penpos4(stem2,0);
x1l=w-x2r=hround(.2w-.5tiny-.5)-eps; y1=y2=h; y3=y4=3/5[y1.bh,y2.bh];
x3l=hround(.5w-.5stem1); x3r=x4r;
z0=whatever[z4l,z2l]=whatever[z1r,z3r];
if hefty: y0:=y2.bh;
 fill z3l--diag_end(3l,1l,1,1,1r,3r){z3r-z1r}
  ...{down}(x0-.5,y0)..(x0+.5,y0){up}...
  {z2l-z4l}diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; % diagonals
else: fill z3l--diag_end(3l,1l,1,1,1r,3r)--z0--
  diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; fi % diagonals
penpos5(stem1,0); x5=x3; y5=0;
fill z3l--diag_end(3l,5l,1,1,5r,3r)--z3r--cycle; %middle stem
z6=whatever[z4,z2]; z7=whatever[z1,z3]; y6=y7=good.y(y1-vair);
fill z1--z7--z6--z2--cycle; % upper bar
% lyus_rbowl;
 numeric stem[],alpha[],right_jut;
 right_jut=if serifs:.6jut else:.4tiny fi;
 y10l=y1.bh; y10r=y2.bh; x10l=x10r=x3; y9=y12=min(bot y10l-.5,.85y10l);
 rt x8r=w-lft x11l=hround(r-letter_fit-.3u-right_jut); bot y8=bot y11=y5;
 stem2=max(tiny.breadth,fudged.stem-3stem_corr);
 alpha2=diag_ratio(1,.5(stem2-tiny),h-y8,x8r-x3);
 penpos8(alpha2*(stem2-tiny),0); penpos11(alpha2*(stem2-tiny),0);
 forsuffixes $=l,r:  y8'$=0; y11'$=0; endfor
 z8'r=z8r+penoffset (x3,h)-z8 of currentpen+whatever*((x3,h)-z8);
 z8'l=z8l+penoffset z8-(x3,h) of currentpen+whatever*((x3,h)-z8);
 z11'r=z11r+penoffset (x3,h)-z11 of currentpen+whatever*((x3,h)-z11);
 z11'l=z11l+penoffset z11-(x3,h) of currentpen+whatever*((x3,h)-z11);
 z8'=.5[z8'l,z8'r];  z11'=.5[z11'l,z11'r];
 z9=z8'+whatever*((x3,h)-z8); z12=z11'+whatever*((x3,h)-z11);
 penpos9(stem-2stem_corr,0); penpos12(stem-2stem_corr,0);
 fill z10l{right}...{z8'l-z9l}z9l--diag_end(9l,8'l,.5,1,8'r,9r)
  --z9r{z9r-z8'r}...{left}z10r--cycle; % right diagonal
 fill z10r{left}...{z11'l-z12l}z12l--diag_end(12l,11'l,1,.5,11'r,12r)
  --z12r{z12r-z11'r}...{right}z10l--cycle; % left diagonal
%%
save slab; % lower.slab
if serifs: numeric inner_jut; prime_points_inside(5,3);
 if rt x5r+jut+.5u+1<=lft x8l-jut: inner_jut=jut;
  else: rt x5r+jut+.5u+1=lft x8l-inner_jut; fi
 slab:=vair; % lower.slab
 dish_serif(11,12,a,1/3,right_jut,b,1/2,inner_jut)(dark); % lower diagonal serif
 dish_serif(8,9,e,1/2,inner_jut,f,1/3,right_jut)(dark); % lower diagonal serif
 dish_serif(5',3,c,1/3,.75jut,d,1/3,.75jut); fi         % lower middle serif
%%%%%
% barh_i_part(8);
_zero:=3;
_one:=13; _two:=14;
%
pickup tiny.nib;
pos[_one](stem,0); pos[_two](stem,0);
lft x[_one]l=lft x[_two]l=
    l+letter_fit+if not monospace: serif_fit+ fi hround (2.5u-.5stem-eps);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
bar_stroke(x[_one],x[_zero]); % bar
if serifs:
   slab:=Vround(vair-vair_corr); % lower.slab
   dish_serif([_one],[_two],s,1/3,jut,t,1/3,jut); % upper serif
   slab:=vair; % lower.slab
   dish_serif([_two],[_one],u,1/3,jut,v,1/3,jut); % lower serif
fi
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12, 13,14,15,16); endchar;

lhchar "Lowercase Cyrillic letter i_e_ac - iot e acute";
cyrchar_twice(i_e_ac,8u#,x_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: 4u# else: 4.5u# fi +serif_fit#, %li_part_adj
 if monospace:.5u# else:.1u# fi);
begingroup
% lower_ukre;
set_bar_axis;
pickup fine.nib; pos2(vair',90); pos3(curve,180); pos4(vair',270);
x2=x4=(.5(w+u)); top y2r=vround(h+1.5oo); bot y4r=-oo;
lft x3r=(hround max(.6u,1.35u-.5curve)); y3=.5h;
if serifs:
 pos1(hair,0); pos5(hair,360);
 y1=min(y2.bh+.75flare-.25vair+eps,.9[bar_height,h]-.75flare);
 rt x1r=rt x5r=hround(w-.5u);
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
 filldraw stroke z1e{x2-x1,5(y2-y1)}...pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
 pos6(.3[fine.breadth,hair],0); x6r=x1r; top y6=h+oo;
 x1r-x1'=1.5curve; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,5(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else:
 pos1(4/7[vair',flare],80); pos5(.6[vair',flare],275);
 rt x1r=hround(w-.6u);
 top y1r=vround .82[bar_height,top y2r];
 filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
 rt x5r=hround(w-.5u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
 if angle direction 1 of p.e>75:
  p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
%%%
% lukre_bar;
if (serifs=true) and (bar#<=.5curve#):
   x7r=x3l; x8r=min(lft x1l-.5hair,0.85[x3l,x1l]);
   y7l=y8l=y1.bh; y7r=y8r=y2.bh; x7l=x7r; x8l=x8r;
   numeric bar[];
   bar2=7/8[bar,fudged.stem-2stem_corr];
   penpos9(bar2,45); z9=.575[z7l,z8l];
   fill circ_stroke z8e{-2,-1}..{-2,1}z9e..{-2,-1}z7e; % curve /\/
else:
   bar_stroke(x3l,min(lft x1l-.5hair,0.85[x3l,x1l]));  % bar --
fi
%%%%%
save slab; % lower.slab
% barh_i_part(3);
_zero:=3;
_one:=10; _two:=11;
%
pickup tiny.nib;
pos[_one](stem,0); pos[_two](stem,0);
lft x[_one]l=lft x[_two]l=
    l+letter_fit+if not monospace: serif_fit+ fi hround (2.5u-.5stem-eps);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
bar_stroke(x[_one],x[_zero]); % bar
if serifs:
   slab:=Vround(vair-vair_corr); % lower.slab
   dish_serif([_one],[_two],s,1/3,jut,t,1/3,jut); % upper serif
   slab:=vair; % lower.slab
   dish_serif([_two],[_one],u,1/3,jut,v,1/3,jut); % lower serif
fi
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,1',2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

% modified numeral 8
lhchar "Lowercase Cyrillic letter uk_ac - uk acute";
cyrchar_twice(uk_ac,if serifs: 9.75u# else:10.5u# fi,
 asc_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
% lower_uk;
numeric top_thickness,mid_thickness,bot_thickness,upper_side,lower_side,theta;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
if serifs:
 bot_thickness=max(fine.breadth,vround(slab-vair_corr));
 upper_side=max(fine.breadth,hround(.5[hair,stem]-stem_corr));
 lower_side=hround(.5[hair,stem]+stem_corr);
else:
 bot_thickness=vair';
 upper_side=max(fine.breadth,hround(.3[vair',stem]-stem_corr));
 lower_side=hround(.3[vair',stem]+stem_corr); fi
 x0=.5w; y0=if hefty:bar_height else:.7x_height fi; %h;%%%%%%%%%%%%%%
pickup fine.nib; theta=90-angle(20u,h); slope:=-h/20u;
if lower_side>1.2upper_side: upper_side:=lower_side; fi
pos1(top_thickness,-90); pos6(bot_thickness,-90);
x1=x6=.5w; top y1l=h+o; bot y6r=-o;
pos0(max(fine.breadth,fudged.stem-2stem_corr),theta);
lft x2l=w-rt x8l=hround 1.25u; lft x7r=w-rt x5r=hround 1.75u if not serifs:+.5u fi;
x2r-x2l=if serifs:fudged.stem else:upper_side fi-fine;
x5r-x5l=lower_side-fine;
ellipse_set(1l,2l,3l,0l); ellipse_set(1r,2r,3r,0r);
ellipse_set(6l,5l,4l,0l); ellipse_set(6r,5r,4r,0r);
numeric tau; tau=max(.8,.20710678/(superness-.5));
y2r:=vround(y2l+max(1,serif_drop)+eps);
filldraw stroke z2e{down}
 ..z3e---z4e..z5e{down}..tension atleast tau..z6e{left};  % S stroke
pos7(lower_side,180); pos8(upper_side,180);
y7=.5[y5l,y5r]; y8=.5[y2l,y2r]; pos9(vair,90); z9=(x0,min(y0,y8-eps));
filldraw stroke z8e{down}
 ..tension atleast tau and atleast 1..{-18u,-.618h}z9e; % upper right stroke
filldraw stroke z6e{left}..tension atleast tau..z7e{up}
 ..tension atleast tau and atleast 1..{18u,.5h}z9e;% lower left stroke
if serifs:
% italic bulb
 z8'=z8; pos8'(upper_side,0);
 y1'+.5curve=h+oo; x1'=x8-max(u,.5u+.5curve);
 numeric theta; theta=angle(4(x8'-x1'),y8'-y1'); pos1'(curve,theta+90);
 filldraw z1'l{dir theta}..tension atleast 1 and 1..{down}z8'l
  --z8'r{up}...{-dir theta}z1'r..cycle;  % bulb
% from sloped serif
 if serif_drop>0:
   pos12(slab,90); lft x12=hround(x2l-max(1,.5jut)-.5-eps);
   top y12r=vround(y2r-max(0,serif_drop)-eps); z14=(x2l,min(y2l-.5,y12)-eps);
   filldraw z2r--z12r--z12l{right}
    ...1/3[(x14,y12l),.5[z12l,z14] ]{z14-z12l}
    ...{down}z14--(x2r,y14)--cycle; % sloped serif
 fi
else: pos10(flare,225); x10r=x1; y10l=h+oo;
 filldraw stroke z8e{up}...z10e; fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once; % cyrcaph for asc_height
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12); endchar;

lhchar "Lowercase Cyrillic letter y_ac - straight u acute";
cyrchar_twice(y_ac,if serifs: 9.5u# else: 9u# fi,x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit#,serif_fit#);
begingroup
% lower_y;
numeric left_stem,right_stem,outer_jut,dy,alpha; left_stem=stem-stem_corr;
right_stem=min(hair if hefty:-2stem_corr fi,left_stem); outer_jut=.75jut;
x1l=w-x4r=l+letter_fit+outer_jut+.05u;
x2l=x5l=hround(.5w-.5left_stem); x3r=x5r; y1=y4=h;
y2=y3=y5=0; dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0); x5=x6; y6=-d;
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>y2+notch_cut: y0:=y2+notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi  % diagonals and stem
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3); prime_points_inside(6,5);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 save slab; slab:=vair; % lower.slab
 dish_serif(6',5,e,1/3,jut,f,1/3,jut);  % lower serif
 slab:=Vround(vair-vair_corr); % lower.slab
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);	   % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);	fi % upper right serif
%%%%%
endgroup;
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,6); endchar;

lhchar "Lowercase Cyrillic letter ae_ac - ae acute";
cyrchar_twice(ae_ac,13u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(0,0);
numeric left_curve,right_curve;
if monospace: right_curve=left_curve=fudged.stem;
else: left_curve=max(tiny.breadth,hround(curve-2stem_corr));
right_curve=max(tiny.breadth,hround(curve-if serifs:6
else:8 fi\\stem_corr));fi
pickup tiny.nib; pos11(right_curve,0);
pos12(vair,90); pos13(mfudged.stem,180);
y11=good.y bar_height; top y12r=h+vround 1.5oo; y10l=bot y11;
rt x11r=hround min(w-.5u,w-u+.5right_curve);
lft x13r=hround (.5w-.5mfudged.stem); x12=.55[x13,x11];
{{interim superness:=more_super;
 filldraw stroke super_arc.e(11,12)}};  % right bowl of e
y13=.5[y12,y14]; bot y14r=-oo; x14=x12+.25u;
if serifs: pos14(vair',270); pos15(mfudged.hair,360);
 y15=max(good.y(.5bar_height-.9),y14l+vair); x15r=x11r;
 (x,y14l)=whatever[z14r,z15]; x14l:=min(x,x14l+.5u);
 filldraw stroke pulled_arc.e(12,13)
  & pulled_super_arc.e(13,14)(.8superpull)
  ...{x15-x14,5(y15-y14)}z15e; % left bowl, arc, and terminal of e
else: pos14(vair,270);
 filldraw stroke super_arc.e(12,13)
  & super_arc.e(13,14);  % left bowl and arc of e
 pickup fine.nib; pos14'(vair,270); z14=z14';
 pos15(.5[vair,flare],275); rt x15r=hround(w-.6u);
 y15r=good.y(y15r+1/3bar_height-y15); y15l:=good.y
 y15l; x15l:=good.x x15l;
 filldraw stroke term.e(14',15,right,1,4); fi  % right terminal
y11'r=y10r=y10l+.6[thin_join,vair]; y11'l=y10l;
x11'l=x11'r=x11; x10l=x10r=x13;
fill stroke z10e--z11'e;  % crossbar
pickup fine.nib; top y3r=h+vround 1.5oo;
if serifs: pos1(flare,180); pos2(mfudged.hair,180);
 pos3(vair,90);  lft x1r=hround max(u,2.1u-.5flare); x3=4u;
 y1=min(bar_height+.5flare+2vair+2,.9[bar_height,h]-.5flare);
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=4.3u; top y1r=vround .82[bar_height,top y3r];
 filldraw stroke term.e(3,1,left,.9,4); fi  % left terminal
pos4(mfudged.stem,0); x4=x13; y4=1/3[bar_height,h];
pos5(mfudged.stem,0); x5=x4; y5=min(y4,y13);
filldraw stroke super_arc.e(3,4)&z4e--z5e;  % arc and stem
pos6(.6[thin_join,vair],90); x6=x4; bot y6=y10l;
pos7(left_curve,180); lft x7r=hround max(.5u,1.5u-.5left_curve);
y7=1/3[top y8l,top y6r];
pos8(vair,270); x8l=3.75u; bot y8r=-oo;
pos9(.5[vair,fudged.stem],360); x9=x5; y9=.55bar_height;
(x',y8r)=whatever[z8l,z9l]; x8r:=max(x',x8-u);
{{interim superness:=more_super;
 filldraw stroke z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
if y9<y5: filldraw stroke z5e{down}..{down}z9e; fi  % link (usually hidden)
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

lhchar "Lowercase Cyrillic letter schwa_ac - schwa acute";
cyrchar_twice(schwa_ac,7.5u#+max(.75u#,.5curve#),x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
% lower_schwa;
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,-180); pos2(vair',-90); pos3(left_curve,0);
set_bar_axis; bot y2r=-oo; y0l=top y1=h-y1.bh;
lft x1r=hround max(.5u,u-.5right_curve);
rt x3r=hround min(w-.5u,w-1.25u+.5left_curve); x2=.5w-.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; top y4r=h+oo; x4=x2;% if not serifs:-.25u fi;
if serifs:
 pos4(vair,90); pos5(hair,180);
 y5=min(bar_height+.25flare+2vair'+2,.9[bar_height,h]-.75flare); %z
 lft x5r=hround(lft x1r+1/3u)+eps;
 (x,y4l)=whatever[z4r,z5]; x4l:=max(x,x4l-.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4); % left bowl, arc,
 pickup fine.nib; pos4'(vair,90); z4'=z4; x4'r:=x4r;
 filldraw stroke z4'e{left}...{x5-x4,5(y5-y4)}z5e; % and terminal
 path upper_arc; upper_arc=z5{x4-x5,5(y4-y5)}..z4{right};
 pos6(.3[fine.breadth,hair],180); x6r=x5r; top y6=h+oo;
 x5'-x5r=2curve-fine; y5'=y5;
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z5'));
 filldraw z5r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else:
 pos4(vair,90);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,90); z4=z4'; pos5(4/7[vair',flare],100);
 lft x5r=hround(tiny.lft x1r+2/3u)+eps;
 top y5r=vround min(top y4r,max(.82[bar_height,top y4r],
   y2.bh+.5(y2.bh-y1.bh)-4/7[vair',flare]*sind 280+eps));
 filldraw stroke term.e(4',5,left,.8,4); fi  % terminal
pickup tiny.nib; path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'l=y0l; x1'l=x1'r=x1; y1'r=y0r=y0l-.6[thin_join,vair];%y1.bh;
forsuffixes $=l,r:
x0$=xpart(((w,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(0,1,2,3,4,5,5',6); endchar;

lhchar "Lowercase Cyrillic letter otld_ac - o tilded acute";
cyrchar_twice(otld_ac,9u#,x_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
% lower_o;tld
penpos1(vair,90); penpos3(vair',-90); penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=h+vround 1.5oo; y3r=-oo;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
%%%
% oltilde;
if (serifs=true) and (bar#<=.5curve#):
   numeric bar[];
   set_bar_axis;
   x5r=x2l; x6r=x4l;
   y5r=y6r=y2.bh; y5l=y6l=y1.bh; x5r=x5l; x6l=x6r;
   bar2=2/3[bar,fudged.stem];
   penpos7(bar2,45); z7=.5[z5l,z6l];
   fill stroke z6e{-2,-1}..{-2,1}z7e..{-2,-1}z5e;
else:
   bar_stroke(x2,x4);
fi
%%%%%
% the accent
hpos_corr=-u; put_accent; repeat_once;
penlabels(1,2,3,4,5,6); endchar;

lhchar "Lowercase Cyrillic letter dje_ac - soft dj acute";
cyrchar_twice(dje_ac,9.5u#,asc_height#,desc_depth#);
italcorr .7x_height#*slant; %o
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
begingroup
pickup tiny.nib; pos1(stem,0); pos2(stem,0); pos3(stem,0);
pos1'(stem',0); pos2'(stem',0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; rt x3r=hround(w-u);
top y1=h; bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
% h_stroke_part;
y3=1/3[bar_height,x_height];
penpos0(x2r-x2l,0); x0=x2; y0=1/8[bar_height,x_height];
filldraw stroke z0e--z2e;  % thicken the lower left stem
penpos2''(min(rt x2r-lft x2l,thin_join)-fine,180);
pickup fine.nib; rt x2''l=tiny.rt x2r; y2''=y0;
pos4(vair,90); pos3'(x3r-x3l+tiny,0); z3'=z3;
x4=.5[rt x2''l,rt x3'r]; top y4r=x_height+oo;
(x,y4l)=whatever[z4r,z2''l]; x4l:=x;
filldraw stroke z2''e{up}...{right}z4e
 & {{interim superness:=hein_super; super_arc.e(4,3')}};  % arch
% h_tail;
pos5(stem,0); pos6(max(fine.breadth,.6[thin_join,vair]),-90);
x5=x3; y5=bar_height; lft x6r=rt x2r; %even with right edge of stem;
bot y6r=-d;
filldraw stroke z3'e--z5e & pulled_arc.e(5,6);
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 save slab; slab:=vair; % lower.slab
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); fi % lower left serif
% hcross;
pickup fine.nib; pos7(bar,90); pos8(bar,90);
x7=0; x8=hround(w-u); y7=y8=vround(.5x_height+.5h)+.5bar;
filldraw stroke z7e--z8e; % bar
endgroup;
% the accent
hpos_corr=-u; put_accent(cyrcaph); repeat_once; % cyrcaph for asc_height
penlabels(1,2,3,4,5,6,7,8); endchar;

endinput;
%end of file
