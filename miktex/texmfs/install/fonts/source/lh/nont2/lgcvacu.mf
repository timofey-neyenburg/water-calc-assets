%
% lgcvacu.mf
%
%% Cyrillic font container with T2 encoding beta-support
%
% This file is future part of lxfonts package
% Version 3.5 // Patchlevel=0
% (c) O.Lapko
%
% This package is freeware product under conditions similar to
% those of D. E. Knuth specified for the Computer Modern family of fonts.
% In particular, only the authors are entitled to modify this file
% (and all this package as well) and to save it under the same name.
%
% Content:
%
% Uppercase Cyrillic acuted vowels
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% List of letternames
%
% A_ac
% E_ac
% YO_ac
% UKRE_ac
% I_ac
% LAT_I_ac
% UKR_I_ac
% O_ac
% ERY_ac
% HRDSN_ac
% EREV_ac
% YU_ac
% YA_ac
% YAT_ac
% IZH_ac
% B_YUS_ac
% L_YUS_ac
% IL_YUS_ac
% IB_YUS_ac
% I_E_ac
% UK_ac
% Y_ac
% AE_ac
% SCHWA_ac
% OTLD_ac
% DJE_ac
%

LHver_check(3,5); % like |version_check| in ec

lhchar "Uppercase Cyrillic letter A_ac - A acute";
cyrchar_twice(A_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(a_ac); fi
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_a;
numeric left_stem,right_stem,outer_jut,alpha; outer_jut=.8cap_jut;
x1l=w-x4r=l+letter_fit+outer_jut+.5u;
right_stem=cap_stem-stem_corr;
left_stem=min(cap_hair if hefty: -3stem_corr fi,right_stem);
y1=y4=0; x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-cap_notch_cut: y0:=h-cap_notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1,z2];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
if hefty: y5r else: y5 fi =5/12y0;
y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut); fi  % right serif
% the accent
if not monospace: hpos_corr=-.75u; fi
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6); endchar;

lhchar "Uppercase Cyrillic letter E_ac - E acute";
cyrchar_twice(E_ac,12u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(e_ac); fi
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
% upper_e;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,h,beak_darkness,1.5beak_jut);  % lower arm and beak
if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

lhchar "Uppercase Cyrillic letter YO_ac - YO acute";
cyrchar_twice(YO_ac,12u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(yo_ac); fi
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
% upper_e;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,h,beak_darkness,1.5beak_jut);  % lower arm and beak
if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
%%%%%
% the accents
uppercase_umlaut(0,0, 10,11,12,13);
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9, 10,11,12,13); endchar;

iff serifs:%
lhchar "Uppercase Cyrillic letter UKR_E_ac - Ukrainian E acute";
cyrchar_twice(UKR_E_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(ukr_e_ac); fi
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
% upper_ukre;
pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
pos3(cap_curve,180); pos4(cap_band,270); pos5(hair,360);
rt x1r=rt x5r=hround(w-u); lft x3r=(hround u); x2=x4=.55[x3,x1];
top y2r=h+o; bot y4r=-o; y3=.5h-vair_corr; y3l:=.52h;
bot y1=min(vround .675h,bot y2l-eps); y5=max(good.y .95(h-y1),y4l+eps);
(x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
(x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
filldraw stroke z1e{x2-x1,10(y2-y1)}...pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)...{up}z5e; % arc
pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
x1r-x1'=2cap_curve-fine; y1'=y1;
path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
% upukre_bar;
numeric bar[]; y7=y8=y3;
x7=x3l; x8=(min(lft x1l-.5hair,0.85[x3l,x1l]));
if (serifs=true) and (bar#<=.5curve#):
 penpos7(cap_bar,90); penpos8(cap_bar,90);
 bar2=7/8[cap_bar,fudged.cap_stem-3stem_corr]; penpos9(bar2,45); z9=.575[z7l,z8l];
 fill circ_stroke z8e{-2,-1}..{-2,1}z9e..{-2,-1}z7e; % curve /\/
else: pickup fine.nib; pos7(cap_bar,90); pos8(cap_bar,90);
 filldraw stroke z7e--z8e; fi % bar --
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6); endchar;

iff not serifs:%
lhchar "Uppercase Cyrillic letter UKR_E_ac - Ukrainian E acute";
cyrchar_twice(UKR_E_ac,11.5u#,cap_height#,0);
if is_small_cap: getcharcode(ukr_e_ac); fi
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
% uppers_ukre;
pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
pos3(cap_curve,180); pos4(slab,270); pos5(flare,275);
rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
lft x3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
top y1r=vround.95h+o; top y2r=h+o; y3=.5h;
bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,3)
 & super_arc.e(3,4) & term.e(4,5,right,.8,4);
% uukres_bar;
numeric bar[]; y6=y7=y3; x6=x3l; x7=(min(lft x1l-.5hair,0.85[x3l,x1l]));
pickup fine.nib;
bar1=if is_small_cap:max(fine.breadth+eps,.6[thin_join,vair]) else:cap_bar fi;
pos6(bar1,90); pos7(bar1,90);
filldraw stroke z6e--z7e;  % bar --
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6); endchar;

lhchar "Uppercase Cyrillic letter I_ac - I acute";
cyrchar_twice(I_ac,13.5u#,cap_height#,0); % N_width
if is_small_cap: getcharcode(i_ac); fi
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_i;
pickup tiny.nib; pos1(fudged.cap_stem,0); pos2(fudged.cap_stem,0);
pos3(fudged.cap_stem,0); pos4(fudged.cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); x3=x4=w-x1;
top y1=top y3=h; bot y2=bot y4=0;
filldraw stroke z1e--z2e; % left stem
filldraw stroke z3e--z4e; % right stem
numeric stem[],alpha[];
if hefty: numeric upper_notch,lower_notch;
 stem1=fudged.hair-stem_corr;
 upper_notch=y6-cap_notch_cut-eps; lower_notch=y5+cap_notch_cut+eps;
 if monospace:
  x5r =rt x1r; x6l =lft x3l; y5=vround(slab+eps); y6=vround(h-slab-eps);
  alpha1=diag_ratio(1,stem1,y5-y6,x5r-x6l);
  penpos5(min(2fudged.cap_stem,alpha1*stem1),0);
  penpos6(min(2fudged.cap_stem,alpha1*stem1),0);
  x5'=lft x1l; z5'=whatever[z5l,z6l]; x6'=rt x4r; z6'=whatever[z5r,z6r];
  x5l:=x5'; x6r:=x6'; y5l:=y5'; y6r:=y6';
 else:
  y5l=0; y6r=h; x5=x1r; x6=x3l; penpos5(stem1,0); penpos6(stem1,0);
 fi
 x2'=rt x1r; z2'=whatever[z5l,z6l]; x3'=lft x4l; z3'=whatever[z5r,z6r];
 fill z5l..
  if y2'>lower_notch: {right}(x2'+1,lower_notch){up}... fi
  {z6-z5}diag_in(5l,6l,1,6r)--z6r..
  if y3'<upper_notch: {left}(x3'-1,upper_notch){down}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonal
else: y5l=y2+bracket+eps; y6r=y3-bracket-eps;
 x5=hround (rt x1r-.5)-eps; x6=hround (lft x3l+.5)+eps;
 stem1=max(tiny.breadth,fudged.hair);
 alpha1=diag_ratio(1,stem1,x6-x5,top y6r-bot y5l);
 penpos5(alpha1*stem1,90); penpos6(alpha1*stem1,90);
 fill stroke z5e--z6e; fi % diagonal
%%%
if serifs: numeric inner_jut;
 if rt x1r+cap_jut+.5u+1<=lft x3l-cap_jut: inner_jut=cap_jut;
  else: rt x1r+inner_jut+.5u+1=lft x3l-inner_jut; fi
 dish_serif(1,2,a,1/3,cap_jut,b,1/3,inner_jut);  % upper left serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,inner_jut);  % lower left serif
 dish_serif(3,4,e,1/3,inner_jut,f,1/3,cap_jut);  % upper right serif
 dish_serif(4,3,g,1/3,inner_jut,h,1/3,cap_jut); fi % lower right serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6); endchar;

lhchar "Uppercase Cyrillic letter LAT_I_ac - Ukrainian I acute";
cyrchar_twice(LAT_I_ac,max(6u#,4u#+cap_stem#),cap_height#,0);
if is_small_cap: getcharcode(lat_i_ac); fi
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_ukri; - borrowed from ec
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs or monospace:
 dish_serif(1,2,a,1/3,1.05cap_jut,b,1/3,1.05cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,1.05cap_jut,d,1/3,1.05cap_jut);  fi % lower serif
if slitex:
 pickup crisp.nib;           % arms based on proportions of uppercase E
 pos3(slab,90); pos4(slab if not serifs:+2stem_corr fi,-90);
 x3=x4=x1; top y3r=h; bot y4r=0;
 pos5(hair,0); pos6(hair,0); % top and bottom right arms
 rt x5r=rt x6r=hround(w-u);
 pos7(hair,180); pos8(hair,180); % top and bottom left arms
 lft x7r=lft x8r=hround(u);
 y5=y7=good.y(y3l-beak)-eps; % beak in case we ever want a beaked serif!
 y6=y8=good.y(y4l+beak)+eps; % watch that this doesn't get too heavy
 arm(3,5,a,darkness,jut); arm(3,7,b,darkness,jut); % top left and right arms
 arm(4,6,c,darkness,jut); arm(4,8,d,darkness,jut); % bottom left and right
fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2); endchar;

lhchar "Uppercase Cyrillic letter UKR_I_ac - YI acute";
cyrchar_twice(UKR_I_ac,max(6u#,4u#+cap_stem#),cap_height#,0);
if is_small_cap: getcharcode(ukr_i_ac); fi
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_ukri; - borrowed from ec
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs or monospace:
 dish_serif(1,2,a,1/3,1.05cap_jut,b,1/3,1.05cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,1.05cap_jut,d,1/3,1.05cap_jut);  fi % lower serif
if slitex:
 pickup crisp.nib;           % arms based on proportions of uppercase E
 pos3(slab,90); pos4(slab if not serifs:+2stem_corr fi,-90);
 x3=x4=x1; top y3r=h; bot y4r=0;
 pos5(hair,0); pos6(hair,0); % top and bottom right arms
 rt x5r=rt x6r=hround(w-u);
 pos7(hair,180); pos8(hair,180); % top and bottom left arms
 lft x7r=lft x8r=hround(u);
 y5=y7=good.y(y3l-beak)-eps; % beak in case we ever want a beaked serif!
 y6=y8=good.y(y4l+beak)+eps; % watch that this doesn't get too heavy
 arm(3,5,a,darkness,jut); arm(3,7,b,darkness,jut); % top left and right arms
 arm(4,6,c,darkness,jut); arm(4,8,d,darkness,jut); % bottom left and right
fi
%%%%%
% the accents
uppercase_umlaut(0,0, 9,10,11,12);
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8, 9,10,11,12); endchar;

lhchar "Uppercase Cyrillic letter O_ac - O acute";
cyrchar_twice(O_ac,14u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(o_ac); fi
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
% upper_o;
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"O"|, not |"0"|
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4); endchar;

lhchar "Uppercase Cyrillic letter U_ac - U acute";
cyrchar_twice(U_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(u_ac); fi
italcorr x_height#*slant+.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_u;
numeric left_stem,right_stem,bot_stem,bot_vair,outer_jut;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
bot_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
bot_vair=Vround(if serifs:.5[vair',bot_stem] else:slab fi);
outer_jut=.7cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.25u; % W jut
y1=y4r=h; x2l=x3l;
if monospace: y2=y3=.3h; else: y2=y3=max(if not serifs:1.095 fi stem,1/6h); fi
numeric alpha; x9=.5w-1/2(.5[bot_stem,right_stem]); y9=-o;
alpha=diag_ratio(2,bot_stem,y1-y3,x4r-x1l-2apex_corr);
numeric u_ratio; u_ratio=(y3-y9)/(y1-y9);
x2l-x1l=x4r-x3r+
   if monospace: 2apex_corr else: if serifs: 2 fi (u_ratio*.5[x1l,x4r]) fi;
penpos3(alpha*bot_stem,0); penpos4(alpha*right_stem,0);
numeric alpha[]; alpha3=(y1++(x2l-x1l))/y1;
penpos1(alpha3*left_stem,0); penpos2(alpha3*left_stem,0);
z0l=whatever[z1r,z2r]=z4l+whatever*(z3r-z4r); penpos0(alpha*bot_stem,0);
z0'=whatever[rt z3r,rt z4r]; y0'=bot y6r=if serifs:-o else: 0 fi;
penpos6(bot_vair,-90); x6r=if serifs:.6 else:.5 fi[x3r,x1];
numeric t; t=if monospace: 1.25right_stem else: (x3r-x0')++(y3r-y0') fi;
penpos5(bot_vair,.5[angle(z4r-z6r),angle(z4r-z3r)]-90);
path p; numeric tau; p=z0r{z3r-z4r}....{left}z6r;
z5r=p intersectionpoint ((0,y2l-.5bot_vair)--(w,y2l-.5bot_vair));
tau=xpart(p intersectiontimes (z2l--z2r));
if y0>notch_cut+y2:
 y0l:=notch_cut+y2; % +y2 added for sanserif face
 fill z0l+.5right{up}...{z4r-z3r}diag_end(0,4l,1,1,4r,3r)
  --subpath(0,tau) of p--z2l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{down}z0l+.5left--cycle; % left and right diagonals
else:
 fill z0l--diag_end(0,4l,1,1,4r,3r)--subpath(0,tau) of p
  --z2l--diag_end(2l,1l,1,1,1r,0)--cycle; % left and right diagonals
fi
fill p--z6l{right}...{-direction tau of p}
     if x5l>x2l: z2l else: z5l fi..{z4r-z3r}z0l--cycle;
pickup fine.nib;
if serifs:
 numeric bulb_diam; bulb_diam=flare+(cap_stem-stem);
 pos6'(bot_vair,-90); z6'=z6; pos7(hair,-180); pos8(bulb_diam,-180);
 y8=1/6h; z7r=z8r; lft x8r=x1;
 cyrbulb(6',7,8);  % arc and bulb
 numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=cap_jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % right serif
else:
 top z6'l=z6l; bot z6'r=z6r;
 pos7(bot_vair,-90); lft x7l=max(3.5u,rt x6r-t); bot y7r=bot y6'r;
 filldraw stroke z6'e--z7e; fi % arc
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;


lhchar "Uppercase Cyrillic letter ERY_ac - ERY acute";
cyrchar_twice(ERY_ac,12.5u#,cap_height#,0); %V_width#=12.5u#
if is_small_cap: getcharcode(ery_ac); fi
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(if monospace: 0,0 else: cap_serif_fit#,cap_serif_fit# fi %uwlett_adj
 +4.5u#); %ERYadj
% upper_ery;
numeric left_stem; left_stem=cap_stem-hround 2stem_corr;
pickup tiny.nib; pos1(left_stem,0); pos2(left_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5left_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
% usftsn_bowl(2);
_zero:=2;
_one:=3; _two:=4; _three:=5; _four:=6; _five:=7;
%
pickup tiny.nib;
penpos[_one](cap_bar,90);
penpos[_three](cap_curve if hefty:-3stem_corr fi,0);
penpos[_four](cap_band,-90); penpos[_five](cap_band,-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_four],y[_two]];
x[_one]=x[_zero]r; x[_three]r=hround(w-u);
if (serifs=false) or (cap_bar#>.5cap_curve#) or (cap_bar<3):
 penpos[_two](cap_bar,90);
 x[_four]=.5[x[_zero],w-if serifs:1.5u else:2.5u fi]+.5u;
 y[_one]=y[_two]=.52h; x[_two]=x[_four];
else: penpos[_two](1/18[cap_bar,cap_curve],90);
 x[_four]=.5[x[_zero],w-1.5u]+.5u;
 y[_one]l=y[_two]l-.35cap_bar=.52h-.5cap_bar;
 x[_two]=x[_four]-.35u; fi
x[_two]l:=x[_two]l-.5u; x[_four]l:=x[_four]l-.5u;
fill stroke z[_five]e..super_arc.e([_four],[_three])
 & super_arc.e([_three],[_two])..z[_one]e; % lobe
%%%
% I
w:=w+4.5u; pickup tiny.nib; rt x8r=rt x9r=w-hround max(2u,3u-.5stem);
top y8=h; bot y9=0; pos8(cap_stem,0); pos9(cap_stem,0);
filldraw stroke z8e--z9e;
if serifs: dish_serif(8,9,e,1/3,cap_jut,f,1/3,cap_jut);
 dish_serif(9,8,g,1/3,cap_jut,h,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,8,9,10,11,12,13,14); endchar;

lhchar "Uppercase Cyrillic letter HRDSN_ac - HARD SIGN acute";
cyrchar_twice(HRDSN_ac,12.5u#,cap_height#,0); %V_width#=12.5u#
if is_small_cap: getcharcode(hrdsn_ac); fi
adjust_fit(min(4.5u#-.5stem#,3.5u#)-.5width_adj#, % Beak_adj
 0);
% upper_hrdsn;
numeric left_stem; left_stem=cap_stem-hround 2stem_corr;
pickup tiny.nib; pos1(left_stem,0); pos2(left_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5left_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
% upper left beak
pickup crisp.nib;
top y3r=h; x3=x1; pos3(slab,90); pos4(hair,180);
lft x4r=l+letter_fit+hround.65u; y4=good.y(y3l-beak)-eps;
arm(3,4,q,beak_darkness,-.7beak_jut);
if serifs: nodish_serif(1,2,a,1/3,.5cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
% usftsn_bowl(2);
_zero:=2;
_one:=5; _two:=6; _three:=7; _four:=8; _five:=9;
%
pickup tiny.nib;
penpos[_one](cap_bar,90);
penpos[_three](cap_curve if hefty:-3stem_corr fi,0);
penpos[_four](cap_band,-90); penpos[_five](cap_band,-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_four],y[_two]];
x[_one]=x[_zero]r; x[_three]r=hround(w-u);
if (serifs=false) or (cap_bar#>.5cap_curve#) or (cap_bar<3):
 penpos[_two](cap_bar,90);
 x[_four]=.5[x[_zero],w-if serifs:1.5u else:2.5u fi]+.5u;
 y[_one]=y[_two]=.52h; x[_two]=x[_four];
else: penpos[_two](1/18[cap_bar,cap_curve],90);
 x[_four]=.5[x[_zero],w-1.5u]+.5u;
 y[_one]l=y[_two]l-.35cap_bar=.52h-.5cap_bar;
 x[_two]=x[_four]-.35u; fi
x[_two]l:=x[_two]l-.5u; x[_four]l:=x[_four]l-.5u;
fill stroke z[_five]e..super_arc.e([_four],[_three])
 & super_arc.e([_three],[_two])..z[_one]e; % lobe
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

iff serifs:%
lhchar "Uppercase Cyrillic letter EREV_ac - REVERSE E acute";
cyrchar_twice(EREV_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(erev_ac); fi
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
% upper_erev;
pickup fine.nib; pos1(cap_hair,180); pos2(cap_band,90);
pos3(cap_curve,0); pos4(cap_band,-90); pos5(hair,-180);
lft x1r=lft x5r=hround u;
rt x3r=hround(w-u); x2=x4=.55[x3,x1];
top y2r=h+o; bot y4r=-o; y3=.5h-vair_corr; y3l:=.52h;
bot y1=min(vround.675h,bot y2l-eps); y5=max(good.y .95(h-y1),y4l+eps);
(x2l',y2l)=whatever[z2r,z1l]; x2l:=max(x2l',x2l-.5u);
(x4l',y4l)=whatever[z4r,z5l]; x4l:=max(x4l',x4l-.5u);
filldraw stroke z1e{x2-x1,10(y2-y1)}...pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)...{up}z5e; % arc
pos6(.3[fine.breadth,cap_hair],180); x6r=x1r; top y6=h+o;
x1'-x1r=2cap_curve-fine; y1'=y1;
path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{right};
numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
%%%
%%% uerev_bar;
numeric bar[]; y8=y7=y3;
x8=x3l; x7=max(rt x1l+.5hair,0.85[x3l,x1l]);
if (bar#<=.5curve#):
   penpos7(cap_bar,90); penpos8(cap_bar,90);
   bar2=7/8[cap_bar,fudged.cap_stem-3stem_corr];
   penpos9(bar2,45); z9=.475[z7l,z8l];
   fill circ_stroke z7e{2,1}..{2,-1}z9e..{2,1}z8e; % curve /\/
else:
   pickup fine.nib; pos7(cap_bar,90); pos8(cap_bar,90);
   filldraw stroke z7e--z8e; % bar --
fi
%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6,10,11,12); endchar;

iff not serifs:%
lhchar "Uppercase Cyrillic letter EREV_ac - REVERSE E acute";
cyrchar_twice(EREV_ac,11.5u#,cap_height#,0);
if is_small_cap: getcharcode(erev_ac); fi
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
% uppers_erev;
pickup fine.nib; pos1(1.2flare,100); pos2(slab,90);
pos3(cap_curve,0); pos4(slab,-90); pos5(flare,-95);
lft x1r=hround1.1u; x2=x4=w-(.5w+1.25u);
rt x3r=w-hround max(u,2u-.5cap_curve);
lft x5r=hround.9u;
top y1r=vround .95h+o; top y2r=h+o; y3=.5h;
bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
filldraw stroke rterm.e(2,1,left,.9,4) & super_arc.e(2,3)
 & super_arc.e(3,4) & term.e(4,5,left,.8,4);
%%%
% urevs_bar;
pickup fine.nib; numeric bar[];
bar1=if is_small_cap: max(fine.breadth+eps,.6[thin_join,vair])
        else: cap_bar fi;
pos6(bar1,90); pos7(bar1,90);
y7=y6=y3; x7=x3l; x6=max(rt x1l+.5hair,0.85[x3l,x1l]);
filldraw stroke z6e--z7e; % bar --
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6,10,11,12); endchar;

lhchar "Uppercase Cyrillic letter YU_ac - YU acute";
cyrchar_twice(YU_ac,14u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(yu_ac); fi
italcorr .7cap_height#*slant-.5u#;
adjust_fit(if monospace: 6.5u# else:
        max(5u#+.5cap_stem#,6u#)+cap_serif_fit# fi, %Ipart_adj
 if monospace:-1.25u# else: 0 fi);
% upper_yu;
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"O"|, not |"0"|
 else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
%I_part(2);
_zero:=2;
_one:=5; _two:=6; _three:=7; _four:=8;
%
pickup tiny.nib; pos[_one](cap_stem,0); pos[_two](cap_stem,0);%pos
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if monospace:-.35u else: cap_serif_fit fi
	+hround max(2u,3u-.5cap_stem);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
x[_three]=x[_one]; y[_three]=y[_zero];
x[_four]=x[_zero]; y[_four]=y[_three];
pos[_three](cap_bar,90); pos[_four](cap_bar,90);
filldraw stroke z[_three]e--z[_four]e; % bar
if serifs: dish_serif([_one],[_two],s,1/3,cap_jut,t,1/3,cap_jut);
 dish_serif([_two],[_one],u,1/3,cap_jut,v,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,10,11,12,13); endchar;

iff not concrete:%
iff not specific:%
lhchar "Uppercase Cyrillic letter YA_ac - YA acute (`roman')";
cyrchar_twice(YA_ac,if serifs:13.5u# else:12.5u#-.5width_adj# fi %N_width; upss R
 ,cap_height#,0); % N_width %
if is_small_cap: getcharcode(ya_ac); fi
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(if serifs:cap_serif_fit# else:0 fi,cap_serif_fit#);
% upperr_ya;
pickup tiny.nib; pos1(cap_stem',0); pos2(cap_stem',0);
rt x1r=rt x2r=hround(w-max(2u,3u-.5cap_stem')); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,180);
numeric middle_weight; middle_weight=.5[vair,cap_band];
penpos6(middle_weight,-90); penpos7(middle_weight,-90);
z3r=top z1; y4=y3; y5=.52[y6l,y4l]; y6=y7;
x7=x2; y7l=vround(.5h+.5vair); x4=x6;
if serifs: x4=.5w+.5u; x5r=hround (2u);
 else: x4=.5w-.5u; x5r=hround u; fi
x4l:=x6l:=x4+.125cap_curve;
fill stroke z7e..pulled_arc.e(6,5) & pulled_arc.e(5,4)..z3e;  % lobe
pickup tiny.nib; numeric stem[],alpha,right_jut;
right_jut=if serifs:.6cap_jut else:.4tiny fi;
if serifs: bot y9=bot y2; else: y9=0; fi
lft x9l=hround(if serifs:l+letter_fit+.75u+right_jut else:.5u fi); y8=y6;
stem1=max(tiny.breadth,fudged.cap_stem-3stem_corr);
alpha=diag_ratio(1,.5(stem1-tiny),y1-y9,x9l-x1);
penpos9(alpha*(stem1-tiny),0);
forsuffixes $=l,r:  y9'$=0;  endfor
z9'r=z9r+penoffset z1-z9 of currentpen+whatever*(z1-z9);
z9'l=z9l+penoffset z9-z1 of currentpen+whatever*(z1-z9);
if serifs: z8r=z9'r+whatever*(z1r-z9); else: x8=x6+.5u; fi
penpos8(if serifs:x9'r-x9'l else: cap_stem-2stem_corr fi,0);
fill z8l--diag_end(8l,9'l,1,.5,9'r,8r)--z8r--cycle;  % diagonal
numeric inner_jut;
if lft x2l-cap_jut-.5u+1>=rt x9r+cap_jut: inner_jut=cap_jut;
 else: lft x2r-cap_jut-.5u+1=rt x9r+inner_jut; fi
if serifs:
 nodish_serif(1,2,a,1/3,.5cap_jut,b,1/3,cap_jut); % upper serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,cap_jut); % lower serif
 dish_serif(9,8,g,1/2,right_jut,h,1/3,inner_jut)(dark); fi % lower diagonal serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

lhchar "Uppercase Cyrillic letter YAT_ac - YAT acute";
cyrchar_twice(YAT_ac,12.5u#,cap_height#,0);
if is_small_cap: getcharcode(yat_ac); fi
italcorr .75cap_height#*slant-.5u#;
adjust_fit(2u#+cap_serif_fit#,0);
% upper_yat;
numeric left_stem,right_curve,middle_weight;
left_stem=cap_stem-hround 2stem_corr; middle_weight=.5[vair,cap_band];
pickup tiny.nib; pos1(left_stem,0); pos2(left_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5left_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
% usftsn_bowl(2);
_zero:=2;
_one:=3; _two:=4; _three:=5; _four:=6; _five:=7;
%
pickup tiny.nib;
penpos[_one](cap_bar,90);
penpos[_three](cap_curve if hefty:-3stem_corr fi,0);
penpos[_four](cap_band,-90); penpos[_five](cap_band,-90);
z[_five]r=bot z[_zero]; y[_four]=y[_five];
y[_three]=.5[y[_four],y[_two]];
x[_one]=x[_zero]r; x[_three]r=hround(w-u);
if (serifs=false) or (cap_bar#>.5cap_curve#) or (cap_bar<3):
 penpos[_two](cap_bar,90);
 x[_four]=.5[x[_zero],w-if serifs:1.5u else:2.5u fi]+.5u;
 y[_one]=y[_two]=.52h; x[_two]=x[_four];
else: penpos[_two](1/18[cap_bar,cap_curve],90);
 x[_four]=.5[x[_zero],w-1.5u]+.5u;
 y[_one]l=y[_two]l-.35cap_bar=.52h-.5cap_bar;
 x[_two]=x[_four]-.35u; fi
x[_two]l:=x[_two]l-.5u; x[_four]l:=x[_four]l-.5u;
fill stroke z[_five]e..super_arc.e([_four],[_three])
 & super_arc.e([_three],[_two])..z[_one]e; % lobe
%%%
% yat beaks
pos8(slab,90); pos9(hair,0);
top y8r=vround .85h; x8=x1; rt x9r=hround(w-2.65u); y9=good.y(y8l-2/3beak)-eps;
arm(8,9,m,beak_darkness,2/3beak_jut);  % right arm and beak
pos10(hair,180); x10=w-x9-4u; y10=y9;
arm(8,10,p,beak_darkness,-2/3beak_jut);  % left arm and beak
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

% for shape, see Knizhnay Shrift p 58  Bodoni, Parma 1818
lhchar "Uppercase Cyrillic letter IZH_ac - IZHITSA acute";
cyrchar_twice(IZH_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(izh_ac); fi
italcorr cap_height#*slant+.25u#;
adjust_fit(cap_serif_fit#,
 if serifs: min(1.5u#,.5u#+(flare#+(cap_stem#-stem#))) %ccL_adj
 else:.5u#fi);
% upper_izh;
numeric left_stem,right_stem,outer_jut,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4=h;
x2-x1=x4-x3; x2l+apex_corr=x3l; y2=y3=-apex_o;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
x6=if serifs:.5[x5,x7r] else:x7r fi;
penpos6(vair,90); y6r=h if serifs:+o fi;
penpos5(alpha*right_stem,0);
y5=min(.9cap_height,bot y6l-.5vair); z5=whatever[z3,z4];
x7r=hround(r-.5u); penpos7(hair,0);
y7=h-vround max(.05fig_height+.5flare,.1fig_height-.5flare)+o; z8r=z7r;
penpos8(flare,0); if serifs: cyrbulb(6,7,8); fi
if y0>cap_notch_cut: y0:=cap_notch_cut;
 fill z0+.5right{up}...{z4-z3}z5l...{right}
      if serifs: z6r--z6l
      else:
         (.5[x4,x4r],y6r)--(fine.lft x6r,y6r)..(x6r,fine.bot y6r)--
         (x6l,fine.top y6l)..(fine.lft x6l,y6l) fi
  {left}
  ....z5r{z3-z4}--diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--z5l{z4-z3}...{right}z6r--z6l{left}...{z3-z4}z5r
  --diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2);
 if rt x1'r+cap_jut+.5u+1<=x4l-cap_jut: inner_jut=cap_jut;
  else: rt x1'r+inner_jut+.5u+1=x4l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut); fi % left serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4); endchar;

iff not specific:%
lhchar "Uppercase Cyrillic letter B_YUS_ac - BIG YUS acute (`roman')";
cyrchar_twice(B_YUS_ac,18u#,cap_height#,0);
if is_small_cap: getcharcode(b_yus_ac); fi
if odd(fudged.cap_stem-w): change_width; fi % symmetric & all three equal
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upperr_yus;
% top_Yus;
% top_Yus;
numeric stem[];
stem1=fudged.cap_stem if hefty:-3stem_corr fi;
stem2=min(stem1,cap_hair if hefty:-2stem_corr fi);
penpos1(stem1,0); penpos2(stem2,0); penpos3(stem1,0); penpos4(stem2,0);
x1l=w-x2r=.2w; y1=h; y2=y1; y3=y4=.52h;
x3l=hround(.5w-.5stem1); x3r=x4r;
z0'=whatever[z4l,z2l]=whatever[z1r,z3r];
if hefty:
 numeric lower_notch; lower_notch=y3+cap_notch_cut;
 fill z3l--diag_end(3l,1l,1,1,1r,3r){z3r-z1r}
 if y0'<=lower_notch: ..{z3r-z1r}z0'{z2l-z4l}..
  else: ...{down}(x0'+.5,lower_notch)--(x0'-.5,lower_notch){up}... fi
 {z2l-z4l}diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; %v
else:
 fill z3l--diag_end(3l,1l,1,1,1r,3r)--z0'--
  diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; fi %v
penpos5(stem1,0); x5=x3; y5=0;
fill z3l--diag_end(3l,5l,1,1,5r,3r)--z3r--cycle; %middle stem
z6=whatever[z4,z2]; z7=whatever[z1,z3]; y6=y7=good.y(y1-slab);
fill z1--z7--z6--z2--cycle; % upper bar
% Yus_rbowl;
pickup tiny.nib; numeric stem[],alpha[],right_jut,middle_weight;
right_jut=.6cap_jut; middle_weight=cap_bar;
penpos10(middle_weight,90);
y10r=y3+.5min(cap_bar,cap_notch_cut); x10=x3; y9=y12=.8y10;
rt x8r=w-lft x11l=hround(r-letter_fit-.3u-right_jut); bot y8=bot y11=y5;
stem2=max(tiny.breadth,fudged.cap_stem-3stem_corr);
alpha2=diag_ratio(1,.5(stem2-tiny),h-y8,x8r-x3);
penpos8(alpha2*(stem2-tiny),0); penpos11(alpha2*(stem2-tiny),0);
forsuffixes $=l,r: y8'$=0; y11'$=0; endfor
z8'r=z8r+penoffset (x3,h)-z8 of currentpen+whatever*((x3,h)-z8);
z8'l=z8l+penoffset z8-(x3,h) of currentpen+whatever*((x3,h)-z8);
z11'r=z11r+penoffset (x3,h)-z11 of currentpen+whatever*((x3,h)-z11);
z11'l=z11l+penoffset z11-(x3,h) of currentpen+whatever*((x3,h)-z11);
z8'=.5[z8'l,z8'r]; z11'=.5[z11'l,z11'r];
z9=z8'+whatever*((x3,h)-z8);  z12=z11'+whatever*((x3,h)-z11);
penpos9(cap_stem-2stem_corr,0); penpos12(cap_stem-2stem_corr,0);
fill z10l{right}...{z8'l-z9l}z9l--diag_end(9l,8'l,.5,1,8'r,9r)
 --z9r{z9r-z8'r}...{left}z10r--cycle; % right diagonal
 fill z10r{left}...{z11'l-z12l}z12l--diag_end(12l,11'l,1,.5,11'r,12r)
  --z12r{z12r-z11'r}...{right}z10l--cycle; % left diagonal
if serifs: numeric inner_jut; prime_points_inside(5,3);
 if rt x5r+cap_jut+.5u+1<=lft x8l-cap_jut: inner_jut=cap_jut;
  else: rt x5r+cap_jut+.5u+1=lft x8l-inner_jut; fi
 dish_serif(11,12,a,1/3,right_jut,b,1/2,inner_jut)(dark); % lower diagonal serif
 dish_serif(8,9,e,1/2,inner_jut,f,1/3,right_jut)(dark); % lower diagonal serif
 dish_serif(5',3,c,1/3,.75jut,d,1/3,.75jut); fi % middle serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15); endchar;

lhchar "Uppercase Cyrillic letter L_YUS_ac - LITTLE YUS acute";
cyrchar_twice(L_YUS_ac,13u#+2cap_stem#,cap_height#,0);
if is_small_cap: getcharcode(l_yus_ac); fi
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_amod;
numeric left_stem,right_stem,outer_jut,alpha; outer_jut=.8cap_jut;
x1l=w-x4r=l+letter_fit+outer_jut+.5u;
%
right_stem=cap_stem-stem_corr;
left_stem=min(cap_hair if hefty: -3stem_corr fi,right_stem); pickup tiny.nib;
y1=y4=0; x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-cap_notch_cut: y0:=h-cap_notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1r,z2r];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3l,z4l]; y6=y5;
if hefty: y5r else: y5 fi =.5y0;
y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
pickup tiny.nib;
x7=.5[x5,x6]; top y7=y5; x8=x7; bot y8=0;
pos7(max(currentbreadth+eps,.5[cap_band,cap_stem]),0);
pos8(max(currentbreadth+eps,.5[cap_band,cap_stem]),0);
filldraw stroke z8e--z7e;
if serifs: pickup tiny.nib; numeric inner_jut;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+.7cap_jut+.5u+1<=lft x8l-.7cap_jut: inner_jut=.7cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x8l-inner_jut; fi
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut);  % right serif
 dish_serif(8,7,e,1/3,inner_jut,f,1/3,inner_jut); fi % middle serif
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8); endchar;

lhchar "Uppercase Cyrillic letter IL_YUS_ac - IOT LITTLE YUS acute";
cyrchar_twice(IL_YUS_ac,13u#+2cap_stem#,cap_height#,0);
if is_small_cap: getcharcode(il_yus_ac); fi
adjust_fit(if monospace: 6.5u# else:
        max(5u#+.5cap_stem#,6u#)+cap_serif_fit# fi, %Ipart_adj
 cap_serif_fit#);
% upper_amod;iot
numeric left_stem,right_stem,outer_jut,alpha; outer_jut=.8cap_jut;
x1l=w-x4r=outer_jut+.5u; %!!!
%
right_stem=cap_stem-stem_corr;
left_stem=min(cap_hair if hefty: -3stem_corr fi,right_stem); pickup tiny.nib;
y1=y4=0; x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-cap_notch_cut: y0:=h-cap_notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1r,z2r];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3l,z4l]; y6=y5;
if hefty: y5r else: y5 fi =.5y0;
y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
pickup tiny.nib;
x7=.5[x5,x6]; top y7=y5; x8=x7; bot y8=0;
pos7(max(currentbreadth+eps,.5[cap_band,cap_stem]),0);
pos8(max(currentbreadth+eps,.5[cap_band,cap_stem]),0);
filldraw stroke z8e--z7e;
if serifs: pickup tiny.nib; numeric inner_jut;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+.7cap_jut+.5u+1<=lft x8l-.7cap_jut: inner_jut=.7cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x8l-inner_jut; fi
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut);  % right serif
 dish_serif(8,7,e,1/3,inner_jut,f,1/3,inner_jut); fi % middle serif
%%%%%
%I_part(9);
_zero:=6;
_one:=9; _two:=10; _three:=11; _four:=12;
%
pickup tiny.nib; pos[_one](cap_stem,0); pos[_two](cap_stem,0);%pos
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if monospace:-.35u else: cap_serif_fit fi
	+hround max(2u,3u-.5cap_stem);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
x[_three]=x[_one]; y[_three]=y[_zero];
x[_four]=x[_zero]; y[_four]=y[_three];
pos[_three](cap_bar,90); pos[_four](cap_bar,90);
filldraw stroke z[_three]e--z[_four]e; % bar
if serifs: dish_serif([_one],[_two],s,1/3,cap_jut,t,1/3,cap_jut);
 dish_serif([_two],[_one],u,1/3,cap_jut,v,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8, 9,10,11,12); endchar;

iff not specific:%
lhchar "Uppercase Cyrillic letter IB_YUS_ac - IOT BIG YUS acute";
cyrchar_twice(IB_YUS_ac,18u#,cap_height#,0);
if is_small_cap: getcharcode(ib_yus_ac); fi
adjust_fit(if monospace: 6.5u# else:
        max(5u#+.5cap_stem#,6u#)+cap_serif_fit# fi, %Ipart_adj
 cap_serif_fit#);
if odd(fudged.cap_stem-w): change_width; fi % symmetric & all three equal
% upperr_yus;
% top_Yus;
numeric stem[];
stem1=fudged.cap_stem if hefty:-3stem_corr fi;
stem2=min(stem1,cap_hair if hefty:-2stem_corr fi);
penpos1(stem1,0); penpos2(stem2,0); penpos3(stem1,0); penpos4(stem2,0);
x1l=w-x2r=.2w; y1=h; y2=y1; y3=y4=.52h;
x3l=hround(.5w-.5stem1); x3r=x4r;
z0'=whatever[z4l,z2l]=whatever[z1r,z3r];
if hefty:
 numeric lower_notch; lower_notch=y3+cap_notch_cut;
 fill z3l--diag_end(3l,1l,1,1,1r,3r){z3r-z1r}
 if y0'<=lower_notch: ..{z3r-z1r}z0'{z2l-z4l}..
  else: ...{down}(x0'+.5,lower_notch)--(x0'-.5,lower_notch){up}... fi
 {z2l-z4l}diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; %v
else:
 fill z3l--diag_end(3l,1l,1,1,1r,3r)--z0'--
  diag_end(4l,2l,1,1,2r,4r)--z4r--cycle; fi %v
penpos5(stem1,0); x5=x3; y5=0;
fill z3l--diag_end(3l,5l,1,1,5r,3r)--z3r--cycle; %middle stem
z6=whatever[z4,z2]; z7=whatever[z1,z3]; y6=y7=good.y(y1-slab);
fill z1--z7--z6--z2--cycle; % upper bar
% Yus_rbowl;
pickup tiny.nib; numeric stem[],alpha[],right_jut,middle_weight;
right_jut=.6cap_jut; middle_weight=cap_bar;
penpos10(middle_weight,90);
y10r=y3+.5min(cap_bar,cap_notch_cut); x10=x3; y9=y12=.8y10;
rt x8r=w-lft x11l=hround(r-letter_fit-.3u-right_jut); bot y8=bot y11=y5;
stem2=max(tiny.breadth,fudged.cap_stem-3stem_corr);
alpha2=diag_ratio(1,.5(stem2-tiny),h-y8,x8r-x3);
penpos8(alpha2*(stem2-tiny),0); penpos11(alpha2*(stem2-tiny),0);
forsuffixes $=l,r: y8'$=0; y11'$=0; endfor
z8'r=z8r+penoffset (x3,h)-z8 of currentpen+whatever*((x3,h)-z8);
z8'l=z8l+penoffset z8-(x3,h) of currentpen+whatever*((x3,h)-z8);
z11'r=z11r+penoffset (x3,h)-z11 of currentpen+whatever*((x3,h)-z11);
z11'l=z11l+penoffset z11-(x3,h) of currentpen+whatever*((x3,h)-z11);
z8'=.5[z8'l,z8'r]; z11'=.5[z11'l,z11'r];
z9=z8'+whatever*((x3,h)-z8);  z12=z11'+whatever*((x3,h)-z11);
penpos9(cap_stem-2stem_corr,0); penpos12(cap_stem-2stem_corr,0);
fill z10l{right}...{z8'l-z9l}z9l--diag_end(9l,8'l,.5,1,8'r,9r)
 --z9r{z9r-z8'r}...{left}z10r--cycle; % right diagonal
 fill z10r{left}...{z11'l-z12l}z12l--diag_end(12l,11'l,1,.5,11'r,12r)
  --z12r{z12r-z11'r}...{right}z10l--cycle; % left diagonal
if serifs: numeric inner_jut; prime_points_inside(5,3);
 if rt x5r+cap_jut+.5u+1<=lft x8l-cap_jut: inner_jut=cap_jut;
  else: rt x5r+cap_jut+.5u+1=lft x8l-inner_jut; fi
 dish_serif(11,12,a,1/3,right_jut,b,1/2,inner_jut)(dark); % lower diagonal serif
 dish_serif(8,9,e,1/2,inner_jut,f,1/3,right_jut)(dark); % lower diagonal serif
 dish_serif(5',3,c,1/3,.75jut,d,1/3,.75jut); fi % middle serif
%%%%%
%I_part(13);
_zero:=10;
_one:=13; _two:=14; _three:=15; _four:=16;
%
pickup tiny.nib; pos[_one](cap_stem,0); pos[_two](cap_stem,0);%pos
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if monospace:-.35u else: cap_serif_fit fi
	+hround max(2u,3u-.5cap_stem);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
x[_three]=x[_one]; y[_three]=y[_zero];
x[_four]=x[_zero]; y[_four]=y[_three];
pos[_three](cap_bar,90); pos[_four](cap_bar,90);
filldraw stroke z[_three]e--z[_four]e; % bar
if serifs: dish_serif([_one],[_two],s,1/3,cap_jut,t,1/3,cap_jut);
 dish_serif([_two],[_one],u,1/3,cap_jut,v,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15, 16,17,18,19);endchar;

iff serifs:%
lhchar "Uppercase Cyrillic letter I_E_ac - IOT E acute";
cyrchar_twice(I_E_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(i_e_ac); fi
italcorr cap_height#*slant-.5u#;
adjust_fit(if monospace: 6u# else:
	max(4.5u#+.5cap_stem#,5.5u#)+cap_serif_fit# fi,0);
% upper_ukre;
pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
pos3(cap_curve,180); pos4(cap_band,270); pos5(hair,360);
rt x1r=rt x5r=hround(w-u); lft x3r=(hround u); x2=x4=.55[x3,x1];
top y2r=h+o; bot y4r=-o; y3=.5h-vair_corr; y3l:=.52h;
bot y1=min(vround .675h,bot y2l-eps); y5=max(good.y .95(h-y1),y4l+eps);
(x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
(x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
filldraw stroke z1e{x2-x1,10(y2-y1)}...pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)...{up}z5e; % arc
pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
x1r-x1'=2cap_curve-fine; y1'=y1;
path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
% upukre_bar;
numeric bar[]; y7=y8=y3;
x7=x3l; x8=(min(lft x1l-.5hair,0.85[x3l,x1l]));
if (serifs=true) and (bar#<=.5curve#):
 penpos7(cap_bar,90); penpos8(cap_bar,90);
 bar2=7/8[cap_bar,fudged.cap_stem-3stem_corr]; penpos9(bar2,45); z9=.575[z7l,z8l];
 fill circ_stroke z8e{-2,-1}..{-2,1}z9e..{-2,-1}z7e; % curve /\/
else: pickup fine.nib; pos7(cap_bar,90); pos8(cap_bar,90);
 filldraw stroke z7e--z8e; fi % bar --
%%%%%
%I_part(3);
_zero:=3;
_one:=10; _two:=11; _three:=12; _four:=13;
%
pickup tiny.nib; pos[_one](cap_stem,0); pos[_two](cap_stem,0);%pos
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if monospace:-.35u else: cap_serif_fit fi
	+hround max(2u,3u-.5cap_stem);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
x[_three]=x[_one]; y[_three]=y[_zero];
x[_four]=x[_zero]; y[_four]=y[_three];
pos[_three](cap_bar,90); pos[_four](cap_bar,90);
filldraw stroke z[_three]e--z[_four]e; % bar
if serifs: dish_serif([_one],[_two],s,1/3,cap_jut,t,1/3,cap_jut);
 dish_serif([_two],[_one],u,1/3,cap_jut,v,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6); endchar;

iff not serifs:%
lhchar "Uppercase Cyrillic letter I_E_ac - IOT E acute";
cyrchar_twice(I_E_ac,11.5u#,cap_height#,0);
if is_small_cap: getcharcode(i_e_ac); fi
italcorr cap_height#*slant-.5u#;
adjust_fit(if monospace: 6.5u# else:
        max(5u#+.5cap_stem#,6u#)+cap_serif_fit# fi, %Ipart_adj
 0);
% uppers_ukre;
pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
pos3(cap_curve,180); pos4(slab,270); pos5(flare,275);
rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
lft x3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
top y1r=vround.95h+o; top y2r=h+o; y3=.5h;
bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,3)
 & super_arc.e(3,4) & term.e(4,5,right,.8,4);
% uukres_bar;
numeric bar[]; y6=y7=y3; x6=x3l; x7=(min(lft x1l-.5hair,0.85[x3l,x1l]));
pickup fine.nib;
bar1=if is_small_cap:max(fine.breadth+eps,.6[thin_join,vair]) else:cap_bar fi;
pos6(bar1,90); pos7(bar1,90);
filldraw stroke z6e--z7e; % bar --
%%%%%
%I_part(3);
_zero:=3;
_one:=10; _two:=11; _three:=12; _four:=13;
%
pickup tiny.nib; pos[_one](cap_stem,0); pos[_two](cap_stem,0);%pos
lft x[_one]l=lft x[_two]l=
	l+letter_fit+if monospace:-.35u else: cap_serif_fit fi
	+hround max(2u,3u-.5cap_stem);
top y[_one]=h; bot y[_two]=0;
filldraw stroke z[_one]e--z[_two]e; % stem
x[_three]=x[_one]; y[_three]=y[_zero];
x[_four]=x[_zero]; y[_four]=y[_three];
pos[_three](cap_bar,90); pos[_four](cap_bar,90);
filldraw stroke z[_three]e--z[_four]e; % bar
if serifs: dish_serif([_one],[_two],s,1/3,cap_jut,t,1/3,cap_jut);
 dish_serif([_two],[_one],u,1/3,cap_jut,v,1/3,cap_jut); fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,1',2,3,4,5,6); endchar;

% Y-o combination
lhchar "Uppercase Cyrillic letter UK_ac - UK acute";
cyrchar_twice(UK_ac,13u#,cap_height#,0);%+if serifs:beak#-.5slab# else:slab# fi
italcorr cap_height#*slant+.25u#; %V
if is_small_cap: getcharcode(uk_ac); fi
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_uk;
numeric left_stem,right_stem,outer_jut,dy,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.05u;
x2l=x5l=hround(.5w-.5left_stem); x3r=x5r; y1=y4=h; y2=y3=y5=.5h; %.4h;
dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0); x5=x6;
y6=.45y5; %0;
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>y2+cap_notch_cut: y0:=y2+cap_notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi  % diagonals and stem
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3); prime_points_inside(6,5);
 if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % upper right serif
 serif(6',5,e,1/3,if serifs:-jut else:.5stem fi); fi  % lower serif
% o
penpos11(vair,90); penpos13(vair',-90);
penpos12(curve,180); penpos14(curve,0);
x12r=hround(l+.5u); %outer_jut+
x14r=w-x12r; x11=x13=.5w; y11r=y5 if not hefty:-vair fi; y13r=-o;
y12=y14=.5y11-vair_corr; y12l:=y14l:=.52y11;
penstroke pulled_super_arc.e(11,12)(.5superpull)
 & pulled_super_arc.e(12,13)(.5superpull)
 & pulled_super_arc.e(13,14)(.5superpull)
 & pulled_super_arc.e(14,11)(.5superpull) & cycle;  % bowl
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,22,3,4,5,6,7,8,88,9); endchar;

lhchar "Uppercase Cyrillic letter Y_ac - straight U acute";
cyrchar_twice(Y_ac,13u#,cap_height#,0);
if is_small_cap: getcharcode(y_ac); fi
italcorr cap_height#*slant+.45u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
% upper_y;
numeric left_stem,right_stem,outer_jut,dy,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.05u;
x2l=x5l=hround(.5w-.5left_stem); x3r=x5r; y1=y4=h; y2=y3=y5=.4h;
dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0); x5=x6; y6=0;
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>y2+cap_notch_cut: y0:=y2+cap_notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi  % diagonals and stem
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3); prime_points_inside(6,5);
 if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut); % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark); % upper right serif
 dish_serif(6',5,e,1/3,cap_jut,f,1/3,cap_jut); fi % lower serif
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6); endchar;

lhchar "Uppercase Cyrillic letter AE_ac - AE acute";
cyrchar_twice(AE_ac,16u#,cap_height#,0);
if is_small_cap: getcharcode(ae_ac); fi
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
numeric left_stem,amid_stem,outer_jut,alpha;
amid_stem=max(tiny.breadth,hround .9[mfudged.hair,mfudged.cap_stem]);
pickup tiny.nib; pos1(amid_stem,0); pos2(amid_stem,0);
lft x1l=lft x2l=hround(if monospace or hefty:.55 else:.5 fi\\w-.75u);
top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(mfudged.hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(mfudged.hair,0); x5=x1;
top y5l=vround(if hefty:.52 else:.48 fi\\[y2,y1]+.5cap_bar);
pos0(cap_bar,90); pos7(mfudged.hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs and not(monospace and hefty): rt x6r=hround(w-3.65u+.5mfudged.hair);
 y6=good.y(y5l+.6beak)+eps; rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90);
pos9(mfudged.hair,0); bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,h,beak_darkness,1.5beak_jut);  % lower arm and beak
left_stem=if monospace:fudged.hair else: cap_hair fi if hefty: -3stem_corr fi;
outer_jut=.8cap_jut; x11l=l+letter_fit+outer_jut+.5u; y11=0;
x12=x1l-apex_corr-if monospace:2 fi\\u; y12=h;
alpha=diag_ratio(1,.5left_stem,y12-y11,x12-x11l);
penpos11(alpha*left_stem,0); penpos12(alpha*left_stem,0);
fill diag_end(12l,11l,1,1,11r,12r)
 --diag_end(11r,12r,1,1,12l,11l)--cycle; % diagonal
y10=h-slab; z10=whatever[z11,z12];
fill z10--(x1,y10)--(x1,h)--z12--cycle;  % link
penpos13(whatever,angle(z2-z1)); z13=whatever[z11,z12];
penpos14(cap_band,90); x14=x0; y13l=y14l; y13r=y14r;
if hefty: y14r=.4h; else: y14=y0; fi
penstroke z13e--z14e; % bar line
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(11,12);
 if rt x11'r+cap_jut+.5u+1<=lft x2l-.75cap_jut: inner_jut=cap_jut;
 else: rt x11'r+inner_jut+.5u+1=lft x2l-.75inner_jut; fi
 dish_serif(11',12,i,1/2,outer_jut,j,.6,inner_jut)(dark);  % lower left serif
 nodish_serif(1,2,a,1/3,cap_jut+x1l-x12,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,.75inner_jut,d,1/3,.5cap_jut); fi  % lower middle serif
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;

lhchar "Uppercase Cyrillic letter SCHWA_ac - SCHWA acute";
cyrchar_twice(SCHWA_ac,if serifs:13u# else:12u# fi,cap_height#,0);
if is_small_cap: getcharcode(schwa_ac); fi
italcorr .7cap_height#*slant-.5u#; %O
adjust_fit(0,0);
% upper_schwa;`large e' shape (ecfonts)
numeric left_curve,right_curve;
left_curve=right_curve+3stem_corr=cap_curve if not serifs: -1.5stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib;
pos1(right_curve,-180); pos2(vair',-90); pos3(left_curve,0);
y1=.48h; bot y2r=-o; y0l=top y1;
if serifs: x2-.25u=x4=.5w-.5u; else: x2=x4=.5w-.25u; fi
lft x1r=hround max(.5u,u-.5right_curve);
rt x3r=w-hround max(.75u,1.5u-.5left_curve); %lower_e+.25u
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}}; % right bowl
y3=.5[y2,y4]; top y4r=h+o;
if serifs:
 pos4(cap_band,90);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4); % left bowl, arc,
 pickup fine.nib; pos4'(cap_band,90); z4'=z4;
 lft x5r=max(hround u,lft x1r+1/3u+eps);
 pos5(cap_hair,180); bot y5=min(vround .675h,bot y4l-eps);
 filldraw stroke z4'e{left}...{x5-x4,10(y5-y4)}z5e; %  and terminal
 pos6(.3[fine.breadth,cap_hair],180); x6r=x5r; top y6=h+o;
 x5'-x5r=2cap_curve-fine; y5'=y5;
 path upper_arc; upper_arc=z5{x4-x5,10(y4-y5)}..{right}z4;
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z5'));
 filldraw z5r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % upper serif
else:
 pos4(slab,90);
 {{interim superness:=min(1.03superness,more_super);
  filldraw stroke super_arc.e(2,3)}};
 filldraw stroke super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(slab,90); z4=z4';
 pos5(1.2flare,100); lft x5r=lft x1r+u; top y5r=vround .95h+o;
 filldraw stroke rterm.e(4',5,left,.75,20); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l-7/8[thin_join,vair']; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r: x0$=xpart(((w,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
%%%%%
% the accent
hpos_corr=-u; put_accent(cyrcaph); repeat_once;
penlabels(0,1,2,3,4,4',5); endchar;

lhchar "Uppercase Cyrillic letter OTLD_ac - O tilded acute";
cyrchar_twice(OTLD_ac,14u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(otld_ac); fi
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
% upper_o;tld
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness; % make |"O"|, not |"0"|
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
%%%%%
% outilde;
z5=z2l; z6=z4l; penpos5(cap_bar,90); penpos6(cap_bar,90);
if (serifs) and (bar#<=.5curve#): numeric bar[];
 bar2=2/3[cap_bar,fudged.cap_stem]; penpos7(bar2,45); z7=.5[z5,z6];
 fill stroke z5e{2,1}..{2,-1}z7e..{2,1}z6e;
else: fill stroke z5e--z6e; fi
%%%%%
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4); endchar;

lhchar "Uppercase Cyrillic letter DJE_ac - Soft DJ acute";
cyrchar_twice(DJE_ac,13u#-width_adj#,cap_height#,0);
if is_small_cap: getcharcode(dje_ac); fi
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,2.5u#); if odd(cap_stem-w): change_width; fi % symmetric
% upper_dje;
% h_stroke_part+bulb
pickup tiny.nib; lft x1l=lft x2l=hround(.5w-.5cap_stem);
bot y2=0; pos2(cap_stem,0);
% upperh_stroke
penpos2''(min(rt x2r-lft x2l,thin_join)-fine,180);
pickup fine.nib; pos3(curve,0); pos4(vair,90);
y3=1/3[bar_height,x_height];
if serifs: x3=r-letter_fit-hround max(1.5u,2.5u-.5curve); %.5u right
 else: rt x3r=r-letter_fit-hround.65u; fi
rt x2''l=tiny.rt x2r; y2''=1/8[bar_height,x_height];
x4=.5[rt x2''l,rt x3r]; top y4r=x_height+o;
(x,y4l)=whatever[z4r,z2''l]; x4l:=x;
filldraw stroke z2''e{up}...{right}z4e
 & {{interim superness:=hein_super; super_arc.e(4,3)}};  % arch
%%%%%
% tail
pos5(curve,0); pos6(vair,-90);
x5=x3; y5=2/3[bot y6r,y3]; bot y6r=-o;
if serifs: numeric bulb_diam; bulb_diam=flare+(cap_stem-stem);
 pos7(hair,-180); pos7'(bulb_diam,-180);
 lft x7r=min(rt x2r+.5cap_jut+.5u,lft x3r-bulb_diam-eps);
 x6=max(lft x7r+.5bulb_diam,.4[lft x7r,rt x5r]);
 y7=1/6cap_height-max(0,flare-stem-2vair_corr); z7r=z7'r;
 cyrbulb(6,7,7'); % bulb
else: pos7(flare,-105);
 x6=.4[lft x7r,rt x5r];
 lft x7r=rt x2r+1.25u+eps; bot y7r=0;
 filldraw stroke term.e(6,7,left,.8,4); fi
filldraw stroke z3e--z5e{down}...pulled_arc.e(5,6);
% T part
pickup tiny.nib; pos1(cap_stem,0); top y1=h;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos8(slab,90); pos9(hair,0);
top y8r=h; x8=x1; rt x9r=hround(w-.65u); y9=good.y(y8l-.7beak)-eps;
arm(8,9,e,beak_darkness,.7beak_jut);  % right arm and beak
pos10(hair,180); x10=w-x9; y10=good.y(y8l-beak)-eps;
arm(8,10,f,beak_darkness,-beak_jut);  % left arm and beak
if serifs: dish_serif(2,1,c,1/3,1.414cap_jut,d,1/3,.5cap_jut);  % lower serif
 nodish_serif(1,2,aa,1/3,.5cap_jut,b,1/3,.5cap_jut); fi  % upper bracketing
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6); endchar;

% T width Tail part
lhchar "Uppercase Cyrillic letter VDJE_ac - Variant Soft DJ acute";
cyrchar_twice(VDJE_ac,13u#-width_adj#,cap_height#,desc_depth#);
if is_small_cap: getcharcode(dje_ac); fi
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,2.5u#); if odd(cap_stem-w): change_width; fi % symmetric
% upper_dje;
full_h:=h; h:=x_height;
% h_stroke_part+bulb
pickup tiny.nib; lft x1l=lft x2l=hround(.5w-.5cap_stem);
bot y2=0; pos2(cap_stem,0);
% upperh_stroke
penpos2''(min(rt x2r-lft x2l,thin_join)-fine,180);
pickup fine.nib; pos3(curve,0); pos4(vair,90);
y3=1/3[bar_height,x_height];
if serifs: x3=r-letter_fit-hround max(1.5u,2.5u-.5curve); %.5u right
 else: rt x3r=r-letter_fit-hround.65u; fi
rt x2''l=tiny.rt x2r; y2''=1/8[bar_height,x_height];
x4=.5[rt x2''l,rt x3r]; top y4r=x_height+o;
(x,y4l)=whatever[z4r,z2''l]; x4l:=x;
filldraw stroke z2''e{up}...{right}z4e
 & {{interim superness:=hein_super; super_arc.e(4,3)}};  % arch
%%%%%
% tail
pos5(curve,0); pos6(vair,-90);
x5=x3; y5=2/3[bot y6r,y3]; bot y6r=-o;
if serifs: numeric bulb_diam; bulb_diam=flare+(cap_stem-stem);
 pos7(hair,-180); pos7'(bulb_diam,-180);
 lft x7r=min(rt x2r+.5cap_jut+.5u,lft x3r-bulb_diam-eps);
 x6=max(lft x7r+.5bulb_diam,.4[lft x7r,rt x5r]);
 y7=1/6cap_height-max(0,flare-stem-2vair_corr); z7r=z7'r;
 cyrbulb(6,7,7'); % bulb
else: pos7(flare,-105);
 x6=.4[lft x7r,rt x5r];
 lft x7r=rt x2r+1.25u+eps; bot y7r=0;
 filldraw stroke term.e(6,7,left,.8,4); fi
filldraw stroke z3e--z5e{down}...pulled_arc.e(5,6);
% T part
h:=full_h; pickup tiny.nib; pos1(cap_stem,0); top y1=h;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos8(slab,90); pos9(hair,0);
top y8r=h; x8=x1; rt x9r=hround(w-.65u); y9=good.y(y8l-.7beak)-eps;
arm(8,9,e,beak_darkness,.7beak_jut);  % right arm and beak
pos10(hair,180); x10=w-x9; y10=good.y(y8l-beak)-eps;
arm(8,10,f,beak_darkness,-beak_jut);  % left arm and beak
if serifs: dish_serif(2,1,c,1/3,1.414cap_jut,d,1/3,.5cap_jut);  % lower serif
 nodish_serif(1,2,aa,1/3,.5cap_jut,b,1/3,.5cap_jut); fi  % upper bracketing
% the accent
put_accent(cyrcaph); repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

endinput;
%end of file
